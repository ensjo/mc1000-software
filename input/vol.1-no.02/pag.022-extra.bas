10  CLEAR 512,16127: REM RESERVA RAM ALTA PARA ROTINA EM CODIGO DE MAQUINA
20  GOSUB 1000: REM INICIALIZA
30  REM CONFIGURA REGISTRADORES FIXOS DO PSG
40 A =  USR (7 * 256 + 64 + 62): REM MIXER (ATIVA CANAL A)
50 A =  USR (8 * 256 + 15): REM VOLUME DO CANAL A
60  REM VARIA FREQUENCIA DO CANAL A
70  FOR I = 255 TO 0 STEP  - 1
80 A =  USR (I)
90  NEXT I
100  REM RESTAURA MASCARA DO REGISTRADOR 7
110 A =  USR (7 * 256 + 64 + 63): REM MIXER (SILENCIA CANAL A)
120  END 
1000  REM INICIALIZA
1010  GOSUB 1100: REM CARREGA ROTINA EM CODIGO DE MAQUINA
1020  CALL 16128: REM INICIALIZA ROTINA E DEFINE FUNCAO USR()
1030  RETURN 
1100  REM CARREGA ROTINA EM CODIGO DE MAQUINA
1110  READ A: REM LE ENDERECO INICIAL
1120  READ B: REM LE BYTE
1130  IF B =  - 1 THEN  RETURN 
1140  POKE A,B
1150 A = A + 1
1160  GOTO 1120
2000  REM DADOS DA ROTINA EM CODIGO DE MAQUINA
2010  DATA 16128      : REM ORG $3E00
2020  REM INIT:
2030  REM ; BLOQUEIA INTERRUPCAO PADRAO
2040  DATA 33,175,197 : REM LD HL,$C5AF;ENDERECO DE INSTRUCAO RETI
2050  DATA 34,57,0    : REM LD (#0039),HL
2060  REM ; CONFIGURA USR()
2070  DATA 33,13,63   : REM LD HL,$3F0D;MYSND
2080  DATA 34,4,3     : REM LD (#0304),HL;DEFUSR
2090  DATA 201        : REM RET
2100  REM MYSND:
2110  DATA 205,240,222: REM CALL $DEF0;DE=ARGUMENTO DE USR()
2120  REM ; D = REGISTRADOR, E = VALOR
2130  DATA 122        : REM LD A,D
2140  DATA 211,32     : REM OUT (REG),A;SELECIONA REGISTRADOR
2150  DATA 123        : REM LD A,E
2160  DATA 211,96     : REM OUT (WR),A;ESCREVE VALOR
2170  DATA 201        : REM RET
2180  DATA -1
9990  REM =====================
9991  REM REVISTA INPUT (BR)
9992  REM VOL.1,NO.1,PAG.22
9993  REM PROGRAMACAO BASIC 2
9994  REM "A ARTE DE FAZER LACOS"
9995  REM (APELO SONORO) (VERSAO EXTRA)
9996  REM VERSAO CCE MC-1000
9997  REM POR EMERSON COSTA
9998  REM EM 2026-02-07
