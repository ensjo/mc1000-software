10  GOSUB 1000: REM INICIALIZA
20  GOSUB 100: REM ENTRADA
30  GOSUB 200: REM CONVERTE ENTRADAS DA BASE
40  GOSUB 300: REM REALIZA OPERACAO
50  GOSUB 400: REM CONVERTE RESULTADO PARA BASE
60  GOSUB 500: REM EXIBE RESULTADO
70  END 
100  REM ENTRADA
110  INPUT "BASE (ATE 36)";B
120  INPUT "NUMERO (INTEIRO)";A$
130  INPUT "SINAL (+,-,*,/)";S$
140  INPUT "NUMERO (INTEIRO)";B$
150  RETURN 
200  REM CONVERTE ENTRADAS
210 P$ = A$: GOSUB 600: REM CONVERTE A$
220 X$ =  STR$ (DE)
230 P$ = B$: GOSUB 600: REM CONVERTE B$
240 Y$ =  STR$ (DE)
250  RETURN 
300  REM REALIZA OPERACAO
310  IF S$ = "*" THEN X =  VAL (X$) *  VAL (Y$)
320  IF S$ = "/" THEN X =  VAL (X$) /  VAL (Y$)
330  IF S$ = "+" THEN X =  VAL (X$) +  VAL (Y$)
340  IF S$ = "-" THEN X =  VAL (X$) -  VAL (Y$)
350  RETURN 
400  REM CONVERTE RESULTADO PARA A BASE
410  REM REJEITA RESULTADO NAO INTEIRO
420  IF X <  >  INT (X) THEN N$ = "RESULTADO NAO INTEIRO": RETURN 
430  REM CONVERTE
435 N$ = "": REM INICIALIZA STRING RESULTADO
440 U = B *  INT (X / B)
450  IF X - U > 9 THEN N$ =  CHR$ (55 + (X - U)) + N$: GOTO 470
460 N$ =  MID$ ( STR$ (X - U) + N$,2)
470 X =  INT (X / B)
480  IF X > 0 THEN  GOTO 440
490  RETURN 
500  REM EXIBE RESULTADO
510  PRINT A$;" ";S$;" ";B$;" = ";N$
520  RETURN 
600  REM CONVERTE NUMERO DA BASE
610 DE = 0: REM VALOR DECIMAL
620 IN = 0: REM INDICE (POTENCIA)
630 Y =  ASC ( RIGHT$ (P$,1)): REM DIGITO MAIS A DIREITA
640  IF Y < 58 THEN D = Y - 48: GOTO 660: REM VALOR DO DIGITO 0-9
650 D = Y - 55: REM VALOR DO DIGITO A-Z (10-35)
660 DE = DE + D * B ^ IN: REM ACUMULA VALOR RELATIVO
670 P$ =  LEFT$ (P$, LEN (P$) - 1): REM REMOVE DIGITO PROCESSADO
680 IN = IN + 1: REM INCREMENTA POTENCIA
690  IF  LEN (P$) > 0 THEN  GOTO 630: REM CONTINUA SE AINDA HA DIGITOS
700  RETURN 
1000  REM INICIALIZA
1010  TEXT : HOME 
1020  RETURN 
9990  REM =====================
9991  REM REVISTA INPUT (BR)
9992  REM VOL.1,NO.2,PAG.35
9993  REM CODIGO DE MAQUINA 2
9994  REM "APRENDA A CONTAR COM UM DEDO SO"
9995  REM (CALCULADORA PARA MULTIPLAS BASES)
9996  REM VERSAO CCE MC-1000
9997  REM POR EMERSON COSTA
9998  REM EM 2026-02-09
