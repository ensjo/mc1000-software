10  GOSUB 1000: REM INICIALIZA
20  GOSUB 100: REM ENTRADA
30  GOSUB 200: REM CONVERTE
40  GOSUB 300: REM EXIBE RESULTADO
50  GOSUB 400: REM PERGUNTA SE CONTINUA
60  GOTO 10
100  REM ENTRADA
110  PRINT : INPUT "DIGITE UM NUMERO ENTRE 0 E 1";N
120  IF N <  = 0 OR N >  = 1 THEN 110: REM REJEITA NUMERO INVALIDO
130  RETURN 
200  REM CONVERTE NUMERO PARA BINARIO
210 N$ = "0.": REM INICIALIZA RESULTADO
220  FOR T = 1 TO 30: REM 30 DIGITOS BINARIOS
230 N = N * 2: REM MULTIPLICA POR 2
240 N$ = N$ +  CHR$ (48 +  INT (N)): REM CONCATENA "0" OU "1"
250 N = N -  INT (N): REM REMOVE PARTE INTEIRA
260  NEXT T
270  RETURN 
300  REM EXIBE RESULTADO
310  PRINT AT$; CHR$ (8); CHR$ (0 + 32);"O NUMERO BINARIO E:"
320  PRINT : PRINT N$: PRINT 
330  RETURN 
400  REM PERGUNTA SE CONTINUA
410  PRINT "OUTRO NUMERO (S/N)?"
420  CALL 49158: REM ESPERA TECLA SER PRESSIONADA E LIBERADA
430 A$ =  CHR$ ( PEEK (283)): REM OBTEM ULTIMA TECLA
440  IF A$ = "S" THEN  RETURN : REM CONTINUA
450  IF A$ = "N" THEN  END : REM TERMINA
460  GOTO 420
1000  REM INICIALIZA
1010  TEXT : HOME 
1020  PRINT "CONVERSAO DE DECIMAL P/ BINARIO"
1030 AT$ =  CHR$ (27) + "=": REM ESCAPE P/ POSICIONAR CURSOR
1040  RETURN 
9990  REM =====================
9991  REM REVISTA INPUT (BR)
9992  REM VOL.1,NO.2,PAG.38
9993  REM CODIGO DE MAQUINA 2
9994  REM "APRENDA A CONTAR COM UM DEDO SO"
9995  REM (CONVERSAO NUMERO FRACIONARIO -> BINARIO)
9996  REM VERSAO CCE MC-1000
9997  REM POR EMERSON COSTA
9998  REM EM 2026-02-09
