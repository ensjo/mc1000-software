10  GOSUB 1000: REM INICIALIZA
20  HOME 
30  GOSUB 100: REM COBRA
40  GOSUB 200: REM SORTEIA
50  GOSUB 300: REM RESULTADO
60  GOSUB 400: REM JOGAR OUTRA VEZ?
70  GOTO 20
100  REM COBRA
110 M = M - 5
120  IF M < 0 THEN  PRINT "DESCULPE... VOCE ESTA DURO!": END 
130  RETURN 
200  REM SORTEIA
210 A =  INT ( RND (1) * 12) + 33
220 B =  INT ( RND (1) * 12) + 33
230 C =  INT ( RND (1) * 12) + 33
240  PRINT AT$; CHR$ (7); CHR$ (14 + 32);: INVERSE : PRINT  CHR$ (A);: NORMAL 
250  PRINT AT$; CHR$ (7); CHR$ (16 + 32);: INVERSE : PRINT  CHR$ (B);: NORMAL 
260  PRINT AT$; CHR$ (7); CHR$ (18 + 32);: INVERSE : PRINT  CHR$ (C): NORMAL 
270  RETURN 
300  REM RESULTADO
310  IF A = B AND B = C THEN  PRINT AT$; CHR$ (11); CHR$ (7 + 32);: INVERSE : PRINT "VOCE GANHOU $50!!!": NORMAL :M = M + 50: RETURN 
320  IF (A = B OR B = C) AND A <  > C THEN  PRINT AT$; CHR$ (11); CHR$ (8 + 32);"VOCE GANHOU $10!":M = M + 10: RETURN 
330  FOR D = 1 TO 500: NEXT D: REM PAUSA
340  RETURN 
400  REM JOGAR OUTRA VEZ?
410  PRINT AT$; CHR$ (13); CHR$ (3 + 32);"QUER JOGAR OUTRA VEZ?(S/N)"
420  PRINT AT$; CHR$ (14); CHR$ (9 + 32);"VOCE TEM $";M
430  CALL 49158: REM ESPERA TECLA
440 K$ =  CHR$ ( PEEK (283))
450  IF K$ = "S" THEN  RETURN 
460  END 
1000  REM INICIALIZA
1010 AT$ =  CHR$ (27) + "=": REM ESCAPE P/ POSICIONAR CURSOR
1020  TEXT : HOME 
1030 M = 50: REM DINHEIRO
1040  RETURN 
9990  REM =====================
9991  REM REVISTA INPUT (BR)
9992  REM VOL.1,NO.3,PAG.43
9993  REM PROGRAMACAO BASIC 3
9994  REM "ENSINE SEU MICRO A TOMAR DECISOES"
9995  REM (CACA-NIQUEIS)
9996  REM VERSAO CCE MC-1000
9997  REM POR EMERSON COSTA
9998  REM EM 2026-02-12
