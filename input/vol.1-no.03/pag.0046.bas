10  CLEAR 512,16204: REM RESERVA RAM ALTA PARA ROTINA EM CODIGO DE MAQUINA
20  GOSUB 1000: REM INICIALIZA SISTEMA
30  GOSUB 600: REM INICIALIZA PROGRAMA
40  GOSUB 300: REM INICIALIZA JOGO
50  GOSUB 100: REM LACO PRINCIPAL
60  GOSUB 400: REM FIM DE JOGO
70  GOTO 40
100  REM LACO PRINCIPAL
110  CALL 51334: REM ATIVA VRAM
120  POKE VR + P, ASC ("*"): REM EXIBE JOGADOR
130 LP = P: REM POSICAO ANTERIOR
140 T = T + 1: REM INCREMENTA TEMPO
150 K$ =  USR (0) INPUT : REM LE TECLA
155  IF K$ = "" THEN 140
160  IF K$ = "A" AND  PEEK (VR + P - 1) < 128 THEN P = P - 1
170  IF K$ = "D" AND  PEEK (VR + P + 1) < 128 THEN P = P + 1
180  IF K$ = "W" AND  PEEK (VR + P - 32) < 128 THEN P = P - 32
190  IF K$ = "S" AND  PEEK (VR + P + 32) < 128 THEN P = P + 32
195  IF P = LP THEN 140
200  IF  PEEK (VR + P) =  ASC (".") THEN D = D + 1: REM COLETA PONTO
210  POKE VR + LP, ASC (" "): REM APAGA POSICAO ANTERIOR
220  IF D <  > DD THEN 120: REM AINDA HA PONTOS A COLETAR
230  POKE VR + P, ASC (" "): REM APAGA JOGADOR
240  CALL 51321: REM DESATIVA VRAM
250  RETURN 
300  REM INICIALIZA JOGO
310 P = P0: REM POSICAO DO JOGADOR
320 D = 0: REM PONTOS COLETADOS
330 T = 0: REM TEMPO
350  REM DESENHA LABIRINTO
360  FOR N = 0 TO 15
365  POKE 910,0: REM EVITA DEFEITO WIDTH
370  PRINT AT$; CHR$ (N); CHR$ (32 + MX);A$(N);
380  NEXT N
390  RETURN 
400  REM FIM DE JOGO
410 TI =  INT (T / 10): REM TEMPO EM SEGUNDOS (APROX.)
415  POKE 910,0: REM EVITA DEFEITO WIDTH
420  PRINT AT$; CHR$ (0); CHR$ (25 + 32);"TEMPO:";
430  PRINT AT$; CHR$ (1); CHR$ (25 + 32);TI;
440  IF FA > TI THEN FA = TI: REM ATUALIZA MELHOR TEMPO
450  PRINT AT$; CHR$ (4); CHR$ (25 + 32);"MELHOR";
451  PRINT AT$; CHR$ (5); CHR$ (25 + 32);"TEMPO:";
460  PRINT AT$; CHR$ (6); CHR$ (25 + 32);FA;
470  PRINT AT$; CHR$ (9); CHR$ (25 + 32);"PRESSI-";
471  PRINT AT$; CHR$ (10); CHR$ (25 + 32);"  ONE";
472  PRINT AT$; CHR$ (11); CHR$ (25 + 32);"<ENTER>";
473  PRINT AT$; CHR$ (12); CHR$ (25 + 32);" PARA";
474  PRINT AT$; CHR$ (13); CHR$ (25 + 32);"COMECAR";
475  PRINT AT$; CHR$ (14); CHR$ (25 + 32);"DE NOVO";
480 K$ =  USR (0) INPUT : REM LE TECLA
485  IF K$ <  >  CHR$ (13) THEN 480
495  POKE 910,0: REM EVITA DEFEITO WIDTH
500  PRINT AT$; CHR$ (9); CHR$ (25 + 32);"       ";
501  PRINT AT$; CHR$ (10); CHR$ (25 + 32);"       ";
502  PRINT AT$; CHR$ (11); CHR$ (25 + 32);"       ";
503  PRINT AT$; CHR$ (12); CHR$ (25 + 32);"       ";
504  PRINT AT$; CHR$ (13); CHR$ (25 + 32);"       ";
505  PRINT AT$; CHR$ (14); CHR$ (25 + 32);"       ";
510  RETURN 
600  REM INICIALIZA PROGRAMA
610 FA = 999999: REM MELHOR TEMPO
620 MX = 6: REM MARGEM ESQUERDA DO LABIRINTO
630 P0 = 0: REM POSICAO INICIAL DO JOGADOR
640 DD = 0: REM PONTOS A COLETAR
650  REM INICIALIZA LABIRINTO
660  DIM A$(15): REM LABIRINTO
670  RESTORE 800
680  FOR N = 0 TO 15
690  READ A$
700  FOR M = 1 TO  LEN (A$)
710 C$ =  MID$ (A$,M,1)
720  IF C$ = "A" THEN C$ =  CHR$ (32 + 128): GOTO 760: REM PAREDE (ESPACO INVERTIDO)
730  IF C$ = "*" THEN P0 = N * 32 + MX + (M - 1): GOTO 760: REM POSICAO INICIAL DO JOGADOR
740  IF C$ = "." THEN DD = DD + 1: GOTO 760
750  PRINT "CARACTER INVALIDO!": END 
760 A$(N) = A$(N) + C$
770  NEXT M,N
780  RETURN 
800  DATA AAAAAAAAAAAAAAAAAAA
810  DATA A.......AAA.......A
820  DATA A.AA.AA.....AA.AA.A
830  DATA A.A.....AAA.....A.A
840  DATA A...A.A.....A.A...A
850  DATA A.AAA.AA.A.AA.AAA.A
860  DATA A...A.........A...A
870  DATA AAA...A.AAA.A...AAA
880  DATA AAA...A.AAA.A...AAA
890  DATA A..*A.........A...A
900  DATA A.AAA.AA.A.AA.AAA.A
910  DATA A...A.A.....A.A...A
920  DATA A.A.....AAA.....A.A
930  DATA A.AA.AA.....AA.AA.A
940  DATA A.......AAA.......A
950  DATA AAAAAAAAAAAAAAAAAAA
1000  REM INICIALIZA
1010  GOSUB 1100: REM CARREGA ROTINA EM CODIGO DE MAQUINA
1020 B =  USR (B) INPUT  RUN : REM ATIVA LEITURA DO TECLADO SEM BLOQUEIO
1030 AT$ =  CHR$ (27) + "=": REM ESCAPE P/ POSICIONAR CURSOR
1040  TEXT : HOME 
1050  POKE 304,201: REM CURSOR SEM PISCAR
1060  POKE 247,202: POKE 248,192: POKE 249,202: POKE 250,192: CALL 52013: REM CURSOR INVISIVEL
1070 VR = 32768: REM ENDERECO DE INICIO DA VRAM
1080  RETURN 
1100  REM CARREGA ROTINA EM CODIGO DE MAQUINA
1110  POKE 836,0: TEXT : HOME : PRINT "INSTALANDO ROTINAS USR. AGUARDE.";
1120  RESTORE 2000
1130  READ E: IF E =  - 1 THEN  PRINT "OK.": RETURN 
1140  READ B: IF B =  - 1 THEN 1130
1150  POKE E,B:E = E + 1: GOTO 1140
2000  REM DADOS DA ROTINA EM CODIGO DE MAQUINA
2010  REM CONFIGURA USR()
2020  DATA 771
2030  DATA 195,77,63,-1
2040  REM PACOTE DE ROTINAS USR
2050  DATA 16205
2060  DATA 225,227,126,254,133,194,225,63,205,160,221,254,138,40,65,254,144,55,40,60,254,148,32,23,205,160,221,229,58,46,1,183,32,5,205,71,195,24,245,205,57,195,225,62,255,24,3,205,39,192,227,50,31,1,183,32,8,33,144,3,54,1,195,29,229,62,1,205,230,230,58,27
2070  DATA 1,42,164,3,119,195,20,231,229,33,32,1,17,176,63,1,179,63,205,228,63,225,205,160,221,227,233,,,,195,182,63,33,14,,57,126,254,93,192,35,126,254,221,192,33,8,,57,54,205,35,54,63,201,193,209,51,51,50,31,1,183,58,27,1,50,28,1,196,223,221,195,97,221
2080  DATA 195,72,216,26,48,7,235,1,254,63,254,1,159,183,192,197,229,1,3,,197,237,176,193,209,225,237,176,201,,-1
2090  DATA -1
9990  REM =====================
9991  REM REVISTA INPUT (BR)
9992  REM VOL.1,NO.1,PAG.46
9993  REM PROGRAMACAO DE JOGOS 3
9994  REM "DIVIRTA-SE COM LABIRINTOS"
9995  REM (COME-COME)
9996  REM VERSAO CCE MC-1000
9997  REM POR EMERSON COSTA
9998  REM EM 2026-02-13
