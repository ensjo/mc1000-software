10  GOSUB 1000: REM INICIALIZA
20  GOSUB 860: REM INICIO DE LINHA
30  GOSUB 100: REM PROXIMA STRING
40  IF C = L THEN  GOTO 30: REM FIM DA STRING?
50  GOSUB 200: REM PROXIMO CARACTER
60  IF C = 1 THEN  GOSUB 300: REM TRATA PRIMEIRO CARACTER
70  IF B$ = " " THEN  GOSUB 400: REM TRATA ESPACO
80  IF B$ <  > "" THEN  GOSUB 500: REM IMPRIME CARACTER
90  GOTO 40
100  REM PROXIMA STRING
110  READ A$: REM LE STRING A IMPRIMIR
120 L =  LEN (A$): REM COMPRIMENTO DA STRING
130 C = 0: REM POSICAO DO CARACTER NA STRING
140  RETURN 
200  REM PROXIMO CARACTER
210 D = D + 1: REM AVANCA POSICAO NA PAGINA
220 C = C + 1: REM AVANCA POSICAO NA STRING
230 B$ =  MID$ (A$,C,1): REM OBTEM CARACTER
240  RETURN 
300  REM TRATA PRIMEIRO CARACTER
310  IF B$ = "=" THEN  PR# 0: END : REM FIM DE IMPRESSAO
320  IF B$ = "#" THEN  GOSUB 600: RETURN : REM ENDERECO
330  IF B$ = "*" THEN  GOSUB 700: RETURN : REM CENTRALIZA
340  IF B$ = "&" THEN  GOSUB 820: GOSUB 930: RETURN : REM QUEBRA LINHA
350  IF B$ = "$" THEN  GOSUB 800: GOSUB 930: RETURN : REM QUEBRA PARAGRAFO
360  REM SEM SIMBOLO NO INICIO DA STRING
370 B$ = " ":A$ = " " + A$:L = L + 1: REM INSERE ESPACO PARA CONTINUAR PARAGRAFO ANTERIOR
380  RETURN 
400  REM TRATA ESPACO (WORD WRAP)
410 SL = LL - D - 1: REM ESPACO RESTANTE NA LINHA APOS O ESPACO
420 CC = C + 1: REM INICIA BUSCA DO PROXIMO ESPACO NA STRING
430 X = 1: REM CONTA CARACTERES ATE O PROXIMO ESPACO
440  IF CC = L THEN 460: REM FIM DA STRING
450  IF  MID$ (A$,CC,1) <  > " " THEN CC = CC + 1:X = X + 1: GOTO 440: REM CONTA CARACTERES NAO ESPACO
460  IF X >  = LL THEN  PRINT : PR# 0: PRINT "ERRO NO FORMATO - PALAVRA MUITO GRANDE": END 
470  IF SL >  = X THEN  RETURN 
480  GOSUB 820: REM QUEBRA DE LINHA
490  GOSUB 930: REM SINALIZA SUPRESSAO DA IMPRESSAO DO ESPACO
495  RETURN 
500  REM IMPRIME CARACTER
510  PRINT B$;
520  IF D > LL THEN  GOSUB 860: REM INICIO DE LINHA
530  RETURN 
600  REM ENDERECO
610  IF D > 1 THEN  PRINT 
620  PRINT  SPC( TR); MID$ (A$,2): REM IMPRIME STRING COM MARGEM DE ENDERECO
630  GOSUB 860: REM INICIO DE LINHA
640  GOSUB 900: REM SINALIZA LEITURA DA PROXIMA STRING
650  GOSUB 930: REM SINALIZA SUPRESSAO DA IMPRESSAO DO CARACTER
660  RETURN 
700  REM CENTRALIZA
710 TA = (LL - L) / 2 + T: REM CALCULA LARGURA DA MARGEM PARA CENTRALIZAR
720  IF TA < T THEN  PRINT CR$;: PR# 0: PRINT "ERRO NO FORMATO - NAO POSSO CENTRALIZAR.": END 
730  IF D > 1 THEN  PRINT 
740  PRINT  SPC( TA); MID$ (A$,2): REM IMPRIME STRING COM MARGEM PARA CENTRALIZAR
750  GOSUB 860: REM INICIO DE LINHA
760  GOSUB 900: REM SINALIZA LEITURA DA PROXIMA STRING
770  GOSUB 930: REM SINALIZA SUPRESSAO DA IMPRESSAO DO CARACTER
780  RETURN 
800  REM QUEBRA PARAGRAFO
810  PRINT 
820  REM QUEBRA LINHA
830  IF D > 1 THEN  PRINT 
840  REM IMPRIME MARGEM
850  PRINT  SPC( T);
860  REM INICIO DE LINHA
870 D = 0
880  RETURN 
900  REM SINALIZA LEITURA DA PROXIMA STRING
910 C = L
920  RETURN 
930  REM SINALIZA SUPRESSAO DA IMPRESSAO DO CARACTER
940 B$ = ""
950  RETURN 
1000  REM INICIALIZA
1010  REM PARAMETROS DE EXECUCAO
1020 PL = 32: REM LARGURA TOTAL DA IMPRESSORA
1030 LL = 30: REM LARGURA DA CARTA
1040  PR# 0: REM DESTINO: 1=TELA,2=IMPRESSORA,3=TELA+IMPRESSORA
1050  REM DEMAIS INICIALIZACOES
1060  TEXT : HOME 
1070 LL = LL + 1
1080 T = (PL - LL) / 2: REM LARGURA DA MARGEM ESQUERDA
1100  REM CALCULA LARGURA DA MARGEM ESQUERDA DE ENDERECO
1110 BE = 0
1120  RESTORE 
1130  READ A$
1140 B$ =  LEFT$ (A$,1)
1150  IF B$ <  > "#" THEN 1190: REM CONSIDERA APENAS STRINGS DE ENDERECO
1160 LE =  LEN (A$) - 1: REM COMPRIMENTO DA LINHA DE ENDERECO SEM "#"
1170  IF LE > LL THEN  PR# 0: PRINT "ERRO NO FORMATO - ENDERECO MUITO LONGO PARA O TAMANHO DE LINHA ESCOLHIDO.": END 
1180  IF LE > BE THEN BE = LE: REM RETEM O MAIOR COMPRIMENTO
1190  IF B$ <  > "=" THEN 1130: REM REPETE O FIM DAS STRINGS
1200  RESTORE : REM RESETA PONTEIRO DE LEITURA
1210 TR = T + LL - BE: REM LARGURA DA MARGEM ESQUERDA DE ENDERECO
1220  RETURN 
5000  DATA "#MARIO DA SILVA"
5010  DATA "#RUA MEXICO, 1245"
5020  DATA "#13100 CAMPINAS, SP"
5030  DATA "# "
5040  DATA "$"
5050  DATA "$SR. ATHOS OLIVEIRA"
5060  DATA "&DIRETOR PRESIDENTE"
5070  DATA "&INDUSTRIA DE OLEOS OLEINA"
5080  DATA "&RUA DAS PALMEIRAS, 111"
5090  DATA "&01045 SAO PAULO, SP"
5100  DATA "$"
5110  DATA "$"
5120  DATA "PREZADO SR. OLIVEIRA:"
5130  DATA "$"
5140  DATA "*REF: PEDIDO DE COLOCACAO"
5150  DATA "$"
5160  DATA "$TENDO TOMADO CONHECIMENTO, ATRAVES DO SEU ANUNCIO PUBLICADO NO"
5170  DATA "ESTADO DE SAO PAULO, DE UMA COLOCACAO IMEDIATA PARA PROGRAMADOR"
5180  DATA "TRAINEE, GOSTARIA DE COMUNICAR A V.SA. QUE ESTOU INTERESSADO NO"
5190  DATA "CARGO OFERECIDO."
5200  DATA "$PARA TANTO, ENVIO EM ANEXO MEU 'CURRICULO VITAE' ATUALIZADO,"
5210  DATA "COM TODAS AS INFORMACOES RELEVANTES A MEU RESPEITO."
5220  DATA "$NO AGUARDO DE UMA COMUNICACAO FAVORAVEL DE SUA PARTE, TERMINO"
5230  DATA "$"
5240  DATA "$"
5250  DATA "ATENCIOSAMENTE"
5260  DATA "$"
5270  DATA "$"
5280  DATA "MARIO DA SILVA"
5290  DATA "="
9990  REM =====================
9991  REM REVISTA INPUT (BR)
9992  REM VOL.1,NO.1,PAG.19
9993  REM APLICACOES 1
9994  REM "ESCREVA CARTAS SEM ESFORCO"
9995  REM VERSAO CCE MC-1000
9996  REM POR EMERSON COSTA
9997  REM EM 2026-02-06
