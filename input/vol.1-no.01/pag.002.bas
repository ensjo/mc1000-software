10  CLEAR 512,16127: REM RESERVA RAM ALTA PARA ROTINA EM CODIGO DE MAQUINA
20  GOSUB 1000: REM CARREGA ROTINA EM CODIGO DE MAQUINA
30  GOSUB 100: REM EXECUTA ROTINA EM BASIC
40  GOSUB 500: REM EXECUTA ROTINA EM CODIGO DE MAQUINA
50  END 
100  REM COPIA LIXO ROM->VRAM USANDO BASIC (LENTO)
110  TEXT : HOME 
120  POKE 304,201: REM DESATIVA PISCAMENTO DO CURSOR
130  PRINT "ISTO ESTA EM BASIC."
140  FOR N = 1 TO 1000: NEXT 
150  OUT 128, PEEK (245) AND  NOT 1: REM ATIVA BANCO DE VRAM
160 V0 = 32768 + 32: REM INICIO DA SEGUNDA LINHA DE TEXTO NA VRAM
170 V1 = 32768 + 512 - 1: REM FIM DA VRAM
180 D = 49152 - V0: REM DIFERENCA ROM<->VRAM
190  FOR N = V0 TO V1
200  POKE N, PEEK (D + N)
210  NEXT N
220  OUT 128, PEEK (245) OR 1: REM DESATIVA BANCO DE VRAM
230  FOR N = 1 TO 1000: NEXT 
240  RETURN 
500  REM COPIA LIXO ROM->VRAM USANDO CODIGO DE MAQUINA
510  HOME 
520  PRINT "ISTO ESTA EM CODIGO DE MAQUINA."
530  FOR N = 1 TO 1000: NEXT 
540  CALL 16128
550  FOR N = 1 TO 2000: NEXT 
560  RETURN 
1000  REM CARREGA ROTINA EM CODIGO DE MAQUINA
1010  READ N: REM LE ENDERECO INICIAL
1020  READ A: REM LE BYTE
1030  IF A =  - 1 THEN  RETURN 
1040  POKE N,A
1050 N = N + 1
1060  GOTO 1020
2000  DATA 16128      : REM ORG $3E00
2010  DATA 205,134,200: REM CALL $C886 ;BANK1
2020  DATA 33,0,192   : REM LD HL,$C000
2030  DATA 17,32,128  : REM LD DE,$8020
2040  DATA 1,224,1    : REM LD BC,$01E0
2050  DATA 237,176    : REM LDIR
2060  DATA 205,121,200: REM CALL $C879 ;BANK0
2070  DATA 201        : REM RET
2080  DATA -1
9990  REM =====================
9991  REM REVISTA INPUT (BR)
9992  REM VOL.1,NO.1,PAG.2
9993  REM CODIGO DE MAQUINA 1
9994  REM "PROGRAMACAO EM CODIGO DE MAQUINA"
9995  REM VERSAO CCE MC-1000
9996  REM POR EMERSON COSTA
9997  REM EM 2026-02-05
