10  CLEAR 512,16127: REM RESERVA RAM ALTA PARA ROTINA Z80
20  GOSUB 1000: REM CARREGA ROTINA Z80
30  GOSUB 100: REM CONFIGURA
40 A =  USR (49152 +  RND (1) * 11000): REM COPIA PARTE DA ROM PARA A VRAM
50  GOTO 40
100  REM CONFIGURA
110  TEXT : HOME 
120  POKE 245,65: REM MODO SEMIGRAFICO 4
130  POKE 772,0: POKE 773,63: REM DEFUSR=16128
140  RETURN 
1000  REM CARREGA ROTINA EM CODIGO DE MAQUINA
1010  READ N: REM ENDERECO INICIAL
1020  READ A: REM BYTE DA ROTINA
1030  IF A =  - 1 THEN  RETURN 
1040  POKE N,A
1050 N = N + 1
1060  GOTO 1020
2000  DATA 16128      : REM ORG $3E00
2010  DATA 205,134,200: REM CALL $C886 ;BANK1 (ATIVA VRAM)
2020  DATA 205,240,222: REM CALL $DEF0 ;DE = ARGUMENTO USR()
2030  DATA 235        : REM EX DE,HL
2040  DATA 17,0,128   : REM LD DE,$8000 ;INICIO DA VRAM
2050  DATA 1,0,2      : REM LD BC,$0200 ;512 BYTES
2060  DATA 237,176    : REM LDIR
2070  DATA 205,121,200: REM CALL $C879 ;BANK0 (DESATIVA VRAM)
2080  DATA 201        : REM RET
2090  DATA -1
9990  REM =====================
9991  REM REVISTA INPUT (BR)
9992  REM VOL.1,NO.1,PAG.3
9993  REM CODIGO DE MAQUINA 1
9994  REM "PROGRAMACAO EM CODIGO DE MAQUINA"
9995  REM VERSAO CCE MC-1000
9996  REM POR EMERSON COSTA
9997  REM EM 2026-02-05
