0  REM ************************
1  REM *     - CCESOFT -      *
2  REM *OTTO LONGO SERNATINGER*
3  REM *  VERSAO 1.1   05/85  *
4  REM ************************
5  REM *    FATORES PRIMOS    *
6  REM ************************
10  HOME : PRINT  TAB( 11)"- CCESOFT -": INVERSE : PRINT : PRINT  TAB( 12)"FATORACAO"; SPC( 10): NORMAL : PRINT 
20  PRINT : PRINT  TAB( 8)"1 < NUMERO < 1E6"
25  PRINT 
30  INPUT "NUMERO ";N
40 N =  INT (N):N1 = N
50  IF N > 1E6 OR N < 2 THEN  RUN 
60  HOME : PRINT : PRINT  TAB( 11)"PENSANDO..."
70 F = 0
80 K = 2: GOSUB 210
90 K = 3: GOSUB 210
100 K = 5: GOSUB 210
110 S =  SQR (N)
120 K = K + 2: IF K > S THEN 270
130  GOSUB 210
140 K = K + 4: GOSUB 210
150 K = K + 2: GOSUB 210
160 K = K + 4: GOSUB 210
170 K = K + 2: GOSUB 210
180 K = K + 4: GOSUB 210
190 K = K + 6: GOSUB 210
200  GOTO 120
210  IF N / K -  INT (N / K) = 0 THEN  GOSUB 230
220  RETURN 
230 U = 1
240 N = N / K: IF N / K -  INT (N / K) = 0 THEN U = U + 1: GOTO 240
250 F = F + 1:K(F) = K:P(F) = U:S =  SQR (N)
260  RETURN 
270  IF N > 1 THEN F = F + 1:K(F) = N:P(F) = 1
280  HOME 
285  PRINT  TAB( 11)"- CCESOFT -": PRINT 
290  PRINT "OS FATORES PRIMOS DE";N1;"SAO:"
295  PRINT " 1 ^ 1"
300  FOR I = 1 TO F
310  PRINT K(I);"^";P(I)
320  NEXT I
330  GOTO 25
