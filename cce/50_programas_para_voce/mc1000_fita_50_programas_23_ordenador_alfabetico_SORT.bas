0  REM ************************
1  REM *     - CCESOFT -      *
2  REM *OTTO LONGO SERNATINGER*
3  REM *  VERSAO 1.1   04/85  *
4  REM ************************
5  REM * ORDENADOR ALFABETICO *
6  REM ************************
10  DATA 205,27,195,58,28,1,50,255,63,201
11  FOR I = 16373 TO 16382
12  READ A
13  POKE I,A
14  NEXT I
20  DIM P$(1000)
25  HOME : PRINT  TAB( 11)"- CCESOFT -": PRINT : INVERSE : PRINT  TAB( 6)"ORDENADOR ALFABETICO      ": NORMAL 
27  PRINT  TAB( 5)"( ATE' 1000 PALAVRAS )": PRINT : PRINT 
28  REM 
29  REM     ENTRADA DE DADOS        
30  FOR I = 1 TO 1000
35  PRINT "PALAVRA #";I;
40  INPUT "";P$(I)
44  REM 
45  REM      DETERMINACAO                    FIM PALAVRAS
46  REM 
48  IF P$(I) = "" THEN 50
49  NEXT I
50  REM OPCAO P/ IMPRESSORA
51  HOME : PRINT : PRINT  TAB( 3)"DESEJA USAR IMPRESSORA S/N ?";
52  CALL 16373
53  IF  CHR$ ( PEEK (16383)) = "N" THEN 61
54  IF  INP (4) = 254 THEN FLAG = 1: GOTO 61
55  HOME : PRINT : PRINT  TAB( 6)"SUA IMPRESSORA ESTA'"
56  PRINT  TAB( 4)"INDEVIDAMENTE CONECTADA": FOR A = 1 TO 2000: NEXT 
61  HOME : PRINT : PRINT  TAB( 11)"- CCESOFT -": PRINT  TAB( 13)"AGUARDE"
64  REM 
65  REM  ALGORITMO DE ORDENACAO
66  REM 
70 Z = 0
80  FOR Q = 1 TO I - 2
90  IF P$(Q) <  = P$(Q + 1) THEN 110
100 Z$ = P$(Q):P$(Q) = P$(Q + 1):P$(Q + 1) = Z$:Z = 1
110  NEXT Q
120  IF Z = 1 THEN 70
122  REM 
123  REM   SAIDA DE DADOS
124  REM 
125  HOME 
126  IF FLAG = 1 THEN  PR# 2
130  FOR W = 1 TO I - 1
140  IF P$(W) <  > "" THEN  PRINT P$(W)
141  IF FLAG = 1 THEN 150
142 LINHA = LINHA + 1: IF LINHA < 13 THEN 150
143 LINHA = 0: PRINT : PRINT "QUALQUER TECLA CONTINUA !";
146  CALL 16373
147 TT$ =  CHR$ ( PEEK (16383)): IF TT$ = "" THEN 146
149  HOME 
150  NEXT W:LINHA = 0
151  IF FLAG = 1 THEN  PRINT  CHR$ (10):FLAG = 0: PR# 0
155  PRINT : PRINT "TOTAL DE PALAVRAS =>";I - 1
156  PRINT : PRINT "MAIS PALAVRAS S/N ?";
157  CALL 16373: IF  CHR$ ( PEEK (16383)) = "N" THEN  END 
158  HOME : FOR Q = I TO 1000
159  PRINT "PALAVRA #";Q;
160  INPUT "";P$(Q)
161  IF P$(Q) = "" THEN I = Q: GOTO 51
162  NEXT Q
