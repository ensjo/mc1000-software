0  REM ************************
1  REM *     - CCESOFT -      *
2  REM *OTTO LONGO SERNATINGER*
3  REM *  VERSAO 1.1   04/85  *
4  REM ************************
5  REM *    NUMEROS PRIMOS    *
6  REM ************************
9  HOME : PRINT  TAB( 12)"- CCESOFT -"
10  INVERSE : PRINT : PRINT  TAB( 10)"NUMEROS PRIMOS"; SPC( 8): NORMAL : PRINT 
20  PRINT  TAB( 4)"MOSTRA OS PRIMEIROS "; CHR$ (34);"N"; CHR$ (34)
30  PRINT  TAB( 9)"NUMEROS PRIMOS"
31  PRINT  TAB( 12)"( N>3 )": PRINT : PRINT 
34  REM 
35  REM  ENTRADA DE DADOS
36  REM 
40  PRINT : INPUT "N ";N
41  REM 
42  REM CONSISTENCIA
43  REM 
45  IF N < 4 THEN  RUN 
46  REM 
47  REM  OPCAO P/ IMPRESSORA
48  REM 
49  PRINT : INPUT "DESEJA USAR IMPRESSORA S/N ";A$
50  IF A$ = "N" THEN 59
51  IF  INP (4) = 254 THEN  PR# 2: GOTO 59
52  HOME : PRINT : PRINT : PRINT  TAB( 6)"SUA IMPRESSORA ESTA'"
53  PRINT  TAB( 4)"INDEVIDAMENTE CONECTADA"
55  FOR A = 1 TO 2000: NEXT 
59  HOME 
60  PRINT "OS";N;"PRIMEIROS NUMEROS PRIMOS:"
70  PRINT : PRINT 1;2;3;
74  REM 
75  REM ALGORITMO
76  REM 
80 I = 3
90  FOR J = 1 TO N - 3
100 I = I + 2
110 K = 3
120 Z =  INT (I / K)
130 R = I - Z * K
140  IF R = 0 THEN 100
150  IF R >  = Z THEN 180
160 K = K + 2
170  GOTO 120
174  REM 
175  REM SAIDA DE DADOS
176  REM 
180  PRINT I;
190  NEXT J
192  REM 
193  REM  INIBE IMPRESSORA
194  REM 
195  PRINT  CHR$ (12): PR# 0
200  REM 
210  REM OPCAO DE VOLTA
220  REM 
230  PRINT : PRINT : INPUT "NOVAMENTE S/N ";A$
240  IF A$ = "S" THEN  CLEAR : HOME : GOTO 40
250  HOME 
