10  HGR
20 X =  INT ( RND (1) * 256)
30 Y =  INT ( RND (1) * 192)
40  SET X,Y
50 WR = Y
60  IF WR > X THEN WR = X
70  IF WR > 191 - Y THEN WR = 191 - Y
80  IF WR > 255 - X THEN WR = 255 - X
90 WR = WR * (1 +  RND (1)) / 2
100  COLOR  = 1: IF  RND (1) > .5 THEN  COLOR  = 0
110  NORMAL : IF  RND (1) > .5 THEN  INVERSE
120  GOSUB 9000
130  NORMAL
140  GOTO 20
8999  END
9000  REM ** DESENHA CIRCULO **
9001  REM POSICIONE O CENTRO COM A INSTRUCAO "SET"
9002  REM WR = RAIO
9003  REM USE INSTRUCAO "INVERSE" PARA PREENCHER.
9004  REM USE INSTRUCAO "COLOR=0" PARA APAGAR.
9010  IF  INT (WR) > 0 THEN 9040
9020  IF  PEEK (246) THEN  DRAW 0,0: RETURN
9030  UNDRAW 0,0: RETURN
9040 WX =  PEEK (858):WY =  PEEK (857)
9050 WO = 2 AND  PEEK (850) <  > 0 OR 1 AND  PEEK (246) = 0
9070 WA = 0:WB =  INT (WR):WP =  INT (3 - 2 * WR)
9080  IF WA > WB THEN  SET WX,WY: RETURN
9090  ON WO GOTO 9200,9300,9400
9100  PLOT WX - WA,WY - WB: PLOT WX + WA,WY - WB
9110  PLOT WX - WB,WY - WA: PLOT WX + WB,WY - WA
9120  PLOT WX - WB,WY + WA: PLOT WX + WB,WY + WA
9130  PLOT WX - WA,WY + WB: PLOT WX + WA,WY + WB
9140  IF WP < 0 THEN WP = WP + 4 * WA + 6:WA = WA + 1: GOTO 9080
9150 WP = WP + 4 * (WA - WB) + 10:WA = WA + 1:WB = WB - 1: GOTO 9080
9200  UNPLOT WX - WA,WY - WB: UNPLOT WX + WA,WY - WB
9210  UNPLOT WX - WB,WY - WA: UNPLOT WX + WB,WY - WA
9220  UNPLOT WX - WB,WY + WA: UNPLOT WX + WB,WY + WA
9230  UNPLOT WX - WA,WY + WB: UNPLOT WX + WA,WY + WB
9240  GOTO 9140
9300  PLOT WX - WA,WY - WB TO WX + WA,WY - WB
9310  PLOT WX - WB,WY - WA TO WX + WB,WY - WA
9320  PLOT WX - WB,WY + WA TO WX + WB,WY + WA
9330  PLOT WX - WA,WY + WB TO WX + WA,WY + WB
9340  GOTO 9140
9400  UNPLOT WX - WA,WY - WB TO WX + WA,WY - WB
9410  UNPLOT WX - WB,WY - WA TO WX + WB,WY - WA
9420  UNPLOT WX - WB,WY + WA TO WX + WB,WY + WA
9430  UNPLOT WX - WA,WY + WB TO WX + WA,WY + WB
9440  GOTO 9140
