1000  REM TREK.BAS   27-JUL-73   ARON K. INSINGA   PROJECT DELTA
1001  REM ADAPTADO PARA MC-1000 POR EMERSON J. S. DA COSTA 15-SEP-2011
1002  DEF  FN FX(X) =  SGN (X) *  INT ( ABS (X)): REM FIX(X)
1060  DIM R(9),D$(6),D(6),G(8,8),Q(8,8),K(9,3)
1070  FOR I = 1 TO 6: READ D$(I): NEXT : FOR I = 1 TO 9: READ R(I): NEXT :I =  RND ( -  PEEK (263))
1080  DATA "MOTORES DE DOBRA","SENS. CURTO ALC.","SENS. LONGO ALC."
1090  DATA "CONTROLE PHASERS","TUBOS TORPEDOS F","CONTROLE AVARIAS"
1100  DATA .0001,.01,.03,.08,.28,1.28,3.28,6.28,13.28
1110 T0 =  INT ( RND (1) * 20 + 20) * 100:T = T0:T9 = 40:E0 = 3000:E = E0:P0 = 10:P = P0:S9 = 200
1130  DEF  FN D(X) =  SQR ((K(I,1) - S1) ^ 2 + (K(I,2) - S2) ^ 2)
1140  DEF  FN R(X) =  INT ( RND (1) * X + 1)
1145  DEF  FN W(X) =  NOT (X < 1 OR X > 8)
1150 Q1 =  FN R(8):Q2 =  FN R(8):S1 =  FN R(8):S2 =  FN R(8)
1160  REM PREPARA GALAXIA
1170 K9 = 0:B9 = 0: FOR I = 1 TO 8: FOR J = 1 TO 8:K3 = 0
1180 C1 =  RND (1) * 64: FOR K0 = 1 TO 9: IF C1 < R(K0) THEN K3 = K3 + 1
1200  NEXT :K9 = K9 + K3:B3 =  ABS ( RND (1) > .9)
1210 B9 = B9 + B3:G(I,J) = K3 * 100 + B3 * 10 +  FN R(8): NEXT J: NEXT I
1220 K0 = K9
1230  IF B9 = 0 THEN I =  FN R(8):J =  FN R(8):G(I,J) = G(I,J) + 10:B9 = 1
1240  HOME : PRINT "ORDENS:   DATA ESTELAR ="; STR$ (T);"."
1250  PRINT "COMO COMANDANTE DA NAVE ESTELAR"
1260  PRINT "ENTERPRISE, SUA MISSAO E LIVRAR"
1265  PRINT "A GALAXIA DA MORTAL AMEACA KLIN-";
1270  PRINT "GON. PARA ISSO, VOCE DEVE DES-"
1275  PRINT "TRUIR A FORCA DE INVASAO KLINGON";
1280  PRINT "COMPOSTA DE";K9;"CRUZADORES DE"
1285  PRINT "BATALHA. VOCE TEM";T9;"ANOS SOLA-"
1290  PRINT "RES PARA COMPLETAR SUA MISSAO."
1295  PRINT "(I.E. ATE A DATA ESTELAR"; STR$ (T0 + T9);".)"
1300  PRINT : PRINT "DE O COMANDO '(F)IM' PARA SAIR"
1305  PRINT "DO JOGO.": PRINT 
1420  PRINT "VOCE ESTA NO QUADRANTE"; STR$ (Q2); STR$ ( - Q1);"."
1430  REM PREPARA QUADRANTE
1440 K3 = 0:B3 = 0:S3 = 0: IF  NOT ( FN W(Q1) AND  FN W(Q2)) GOTO 1460
1445  FOR I = 1 TO 9: FOR J = 1 TO 3:K(I,J) = 0: NEXT J,I
1450 X = G(Q1,Q2):K3 =  INT (X / 100):B3 =  INT (X / 10) - 10 * K3:S3 = X -  INT (X / 10) * 10
1460  FOR I = 1 TO 8: FOR J = 1 TO 8:Q(I,J) = 0: NEXT J,I:Q(S1,S2) = 1
1470  IF K3 THEN  FOR I = 1 TO K3: GOSUB 2560:Q(R1,R2) = 2:K(I,1) = R1:K(I,2) = R2:K(I,3) = S9: NEXT I
1480  IF B3 THEN  FOR I = 1 TO B3: GOSUB 2560:Q(R1,R2) = 3: NEXT I
1490  IF S3 THEN  FOR I = 1 TO S3: GOSUB 2560:Q(R1,R2) = 4: NEXT I
1500 A = 0
1510  REM TOMADA DOS SENSORES DE CURTO ALCANCE
1520 C$ = "VERDE": IF E <  = E0 / 10 THEN C$ = "AMARELA": GOTO 1530
1521  FOR I = Q1 - 1 TO Q1 + 1: FOR J = Q2 - 1 TO Q2 + 1
1522  IF  FN W(I) AND  FN W(J) THEN  IF G(I,J) > 99 THEN C$ = "AMARELA":I = 10:J = 10
1525  NEXT J,I
1530  FOR I = S1 - 1 TO S1 + 1: FOR J = S2 - 1 TO S2 + 1: IF I < 1 OR I > 8 OR J < 1 OR J > 8 GOTO 1560
1540  IF Q(I,J) = 3 THEN C$ = "ATRACADA":E = E0:P = P0: GOTO 1570
1560  NEXT J,I: IF K3 THEN C$ = "VERMELHA"
1565  IF A = 0 THEN  GOSUB 2370
1570  IF D(2) THEN  PRINT "OS SENSORES DE CURTO ALCANCE": PRINT "ESTAO INOPERANTES.": GOTO 1650
1580  PRINT : PRINT : PRINT "---------------": FOR I = 1 TO 8
1590  FOR J = 1 TO 8: PRINT  MID$ (".EKB*",Q(I,J) + 1,1);" ";: NEXT J: ON I GOSUB 1610,1615,1620,1625,1630,1635,1640,1645
1600  NEXT I: PRINT "---------------": PRINT 
1605  GOTO 1650
1610  PRINT "DATA E.:"; STR$ (T): RETURN 
1615  PRINT "COND.: ";C$: RETURN 
1620  PRINT "QUADRANTE: "; STR$ (Q2); STR$ ( - Q1): RETURN 
1625  PRINT "SETOR:     "; STR$ (S2); STR$ ( - S1): RETURN 
1630  PRINT "ENERGIA:  "; STR$ (E): RETURN 
1635  PRINT "TORPEDOS F.:"; STR$ (P): RETURN 
1640  PRINT "KLINGONS:   "; STR$ (K9): RETURN 
1645  PRINT : RETURN 
1650  REM COMANDO
1660 A$ = "": PRINT 
1670  INPUT "COMANDO";A$:A = 0: FOR I = 1 TO 7: IF  MID$ ("NCLPTAF",I,1) =  LEFT$ (A$,1) THEN A = I:I = 7
1675  NEXT I
1680  ON A GOTO 1700,1510,1960,2040,2140,2310,2570
1690  PRINT "COMANDOS: (N)AVEGACAO, SENSORES": PRINT "DE (C)URTO ALCANCE, SENSORES DE": PRINT "(L)ONGO ALCANCE, (P)HASERS,": PRINT "(T)ORPEDOS, (A)VARIAS, (F)IM.": GOTO 1670
1700  REM DOBRA ESPACIAL
1710  INPUT "DIRECAO (1-8.99999)";C1: INPUT "FATOR DE DOBRA (0-12)";W1
1720  IF W1 * C1 = 0 GOTO 1650
1725  IF C1 < 1 OR C1 >  = 9 OR W1 < 0 OR W1 > 12 GOTO 1710
1730  IF W1 > .2 AND D(1) < 0 THEN  PRINT "MOTORES DE DOBRA AVARIADOS,": PRINT "VELOCIDADE MAXIMA = DOBRA 0.2.": GOTO 1710
1740  IF K3 > 0 THEN  GOSUB 2370
1750  FOR I = 1 TO 6: IF D(I) < 0 THEN D(I) = D(I) + 1
1755  NEXT : IF  RND (1) > .20 GOTO 1830
1760  IF  RND (1) > .5 GOTO 1790
1765 I =  FN R(6)
1770  PRINT "*** TEMPESTADE ESPACIAL,": PRINT D$(I);" AVARIADO. ***"
1780 D(I) = D(I) -  FN R(5): GOTO 1830
1790  FOR I = 1 TO 6: IF D(I) GOTO 1810
1795  NEXT : GOTO 1830
1810 D(I) = D(I) -  FN FX( RND (1) * D(I) - 1): IF D(I) > 0 THEN D(I) = 0
1820  PRINT "*** TREGUA, SITUACAO DE REPARO": PRINT "DE ";D$(I);" MELHORADA. ***"
1830 T = T + 1:N =  INT (W1 * 8):E = E - N: IF E <  = 0 OR T > T0 + T9 GOTO 2450
1840 Q(S1,S2) = 0:X = S1:Y = S2: GOSUB 2300
1860  FOR I = 1 TO N:X = X + X1:Y = Y + X2:Z1 =  INT (X + .5):Z2 =  INT (Y + .5)
1870  IF  NOT ( FN W(Z1) AND  FN W(Z2)) GOTO 1920
1871  IF Q(Z1,Z2) GOTO 1880
1872  NEXT I
1874 S1 =  INT (X + .5):S2 =  INT (Y + .5):Q(S1,S2) = 1
1875  IF S1 < 1 THEN S1 = 1
1876  IF S1 > 8 THEN S1 = 8
1877  IF S2 < 1 THEN S2 = 1
1878  IF S2 > 8 THEN S2 = 8
1879  GOTO 1510
1880  PRINT "ENTERPRISE BLOQUEADA POR OBJETO": PRINT "NO SETOR"; STR$ (Z2); STR$ ( - Z1);"."
1885 X = X - X1:Y = Y - X2: GOTO 1874
1920 X = Q1 + W1 * X1 + (S1 - .5) / 8:Y = Q2 + W1 * X2 + (S2 - .5) / 8
1925 Z1 =  INT (X):Z2 =  INT (Y):S1 =  INT (8 * (X - Z1) + .5):Y =  INT (8 * (Y - Z2) + .5)
1930  IF Z1 > 8 THEN Q1 = 8: GOTO 1940
1931  IF Z1 < 1 THEN Q1 = 1: GOTO 1940
1932 Q1 = Z1
1940  IF Z2 > 8 THEN Q2 = 8: GOTO 1950
1941  IF Z2 < 1 THEN Q2 = 1: GOTO 1950
1942 Q2 = Z2
1950  GOTO 1430
1960  REM TOMADA DOS SENSORES DE LONGO ALCANCE
1970  IF D(3) THEN  PRINT "OS SENSORES DE LONGO ALCANCE": PRINT "ESTAO INOPERANTES.": GOTO 1650
1980  PRINT "SONDAGEM DOS SENSORES DE LONGO": PRINT "ALCANCE PARA O QUADRANTE"; STR$ (Q2); STR$ ( - Q1);"."
1990  PRINT : FOR I = Q1 - 1 TO Q1 + 1: FOR J = Q2 - 1 TO Q2 + 1: PRINT "  ";
2000  IF  NOT ( FN W(I) AND  FN W(J)) THEN  PRINT "000";: GOTO 2030
2010 A$ =  STR$ (G(I,J)):A$ =  MID$ (A$,2)
2020  PRINT  LEFT$ ("00",3 -  LEN (A$));A$;
2030  NEXT J: PRINT : NEXT I: GOTO 1650
2040  REM CONTROLE DE PHASERS
2050  IF D(4) THEN  PRINT "CONTROLE DE PHASERS AVARIADO.": GOTO 1650
2060  PRINT "PHASERS TRAVADOS NO ALVO.": PRINT "ENERGIA DISPONIVEL ="; STR$ (E);"."
2070  INPUT "UNIDADES A DISPARAR";X
2080 X =  INT (X): IF X <  = 0 GOTO 1650
2081  IF X > E GOTO 2060
2082 E = E - X
2090  FOR I = 1 TO 9: IF K(I,3) <  = 0 GOTO 2130
2100 H =  INT (X /  FN D(0) * (2 +  RND (1))):K(I,3) = K(I,3) - H
2110  PRINT "KLINGON NO SETOR"; STR$ (K(I,2)); - K(I,1);"ATINGIDO,": PRINT  - H;"UNIDADES, ";
2120  IF K(I,3) > 0 THEN  PRINT "RESTAM"; STR$ (K(I,3));".": GOTO 2130
2124  PRINT "DESTRUIDO!"
2126 K3 = K3 - 1:K9 = K9 - 1:Q(K(I,1),K(I,2)) = 0
2128 G(Q1,Q2) = G(Q1,Q2) - 100: IF K9 <  = 0 GOTO 2500
2130  NEXT I: GOSUB 2370: GOTO 1650
2140  REM TORPEDOS DE FOTON
2150  IF D(5) THEN  PRINT "OS TUBOS DE DISPARO DE TORPEDOS": PRINT "DE FOTON ESTAO INOPERANTES.": GOTO 1650
2160  IF P <  = 0 THEN  PRINT "TODOS OS TORPEDOS DE FOTON JA": PRINT "FORAM DISPARADOS.": GOTO 1650
2170  INPUT "DIRECAO DO TORPEDO (1-8.99999)";C1
2180  IF C1 = 0 GOTO 1650
2181  IF C1 < 1 OR C1 >  = 9 GOTO 2170
2182  GOSUB 2300
2190 X = S1:Y = S2:P = P - 1: PRINT : PRINT "TRAJETO DO TORPEDO:"
2200 X = X + X1:Y = Y + X2:Z1 =  INT (X + .5):Z2 =  INT (Y + .5)
2210  IF  NOT ( FN W(Z1) AND  FN W(Z2)) THEN  PRINT "PERDIDO.": GOTO 2290
2220  PRINT  STR$ ( INT (Y * 10 + .5) / 10); -  INT (X * 10 + .5) / 10: IF Q(Z1,Z2) = 0 GOTO 2200
2230  IF Q(Z1,Z2) <  > 2 GOTO 2260
2231  PRINT "*** KLINGON DESTRUIDO. ***"
2240  FOR I = 1 TO 9: IF K(I,1) = Z1 AND K(I,2) = Z2 THEN K(I,3) = 0
2241  NEXT 
2250 K3 = K3 - 1:K9 = K9 - 1: IF K9 <  = 0 GOTO 2500
2251  GOTO 2280
2260  IF Q(Z1,Z2) = 4 THEN  PRINT "ESTRELA DESTRUIDA.":S3 = S3 - 1: GOTO 2280
2270  PRINT "*** BASE ESTELAR DESTRUIDA. ***": PRINT "PARABENS...":B3 = B3 - 1
2280 Q(Z1,Z2) = 0:G(Q1,Q2) = K3 * 100 + B3 * 10 + S3
2290  GOSUB 2370: GOTO 1650
2300 X1 =  -  SIN ((C1 - 1) * .785398):X2 =  COS ((C1 - 1) * .785398): RETURN 
2310  REM RELATORIO DE CONTROLE DE AVARIAS
2320  IF D(6) GOTO 1650
2321  PRINT : PRINT "DISPOSITIVO"; TAB( 20);"ESTADO": PRINT 
2330  FOR I = 1 TO 6: PRINT D$(I); TAB( 20);D(I): NEXT : GOTO 1650
2370  REM  ATAQUE KLINGON
2380  IF K3 = 0 THEN  RETURN 
2390  IF C$ = "ATRACADA" THEN  PRINT "OS ESCUDOS DA BASE ESTELAR": PRINT "PROTEGEM A ENTERPRISE.": RETURN 
2400  PRINT : FOR I = 1 TO 9: IF K(I,3) <  = 0 GOTO 2440
2410 H =  INT ((K(I,3) /  FN D(0)) * (2 +  RND (1))) + 1:E = E - H
2420  PRINT "ATAQUE DE KLINGON NO SETOR"; STR$ (K(I,2)); STR$ ( - K(I,1));","
2430  PRINT  - H;"UNIDADES, RESTAM"; STR$ (E);"."
2440  NEXT I: IF E > 0 THEN  RETURN 
2450  PRINT : PRINT "E A DATA ESTELAR"; STR$ (T);".": PRINT 
2460  PRINT "A ENTERPRISE FOI DESTRUIDA."
2470  PRINT "A FEDERACAO FOI CONQUISTADA."
2480  PRINT "AINDA HA";K9;"CRUZADORES DE BATA-"
2485  PRINT "LHA KLINGON."
2490  PRINT "VOCE ESTA MORTO.": GOTO 2570
2500  PRINT : PRINT "E A DATA ESTELAR"; STR$ (T);".": PRINT 
2510  PRINT "O ULTIMO CRUZADOR DE BATALHA"
2515  PRINT "KLINGON NA GALAXIA FOI DESTRU-": PRINT "IDO"
2520  PRINT "A FEDERACAO FOI SALVA."
2530  PRINT "VOCE FOI PROMOVIDO A ALMIRANTE."
2540  PRINT K0;"KLINGONS EM";T - T0;"ANOS."
2545  PRINT "AVALIACAO ="; STR$ ( INT (K0 / (T - T0) * 1000));"."
2550  GOTO 2570
2560 R1 =  FN R(8):R2 =  FN R(8): IF Q(R1,R2) GOTO 2560
2565  RETURN 
2570  END 
