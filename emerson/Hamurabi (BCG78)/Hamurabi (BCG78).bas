0  HOME 
10  PRINT : PRINT  TAB( 12);: INVERSE : PRINT "HAMURABI": NORMAL : PRINT 
20  PRINT "       CREATIVE COMPUTING"
25  PRINT "     MORRISTOWN, NEW JERSEY"
30  PRINT : PRINT : PRINT 
40  PRINT "   TESTE SUA CAPACIDADE COMO"
50  PRINT " GOVERNADOR DA  ANTIGA SUMERIA"
60  PRINT "   EM UM MANDATO DE DEZ ANOS."
70  PRINT : PRINT : PRINT 
80  INPUT "         TECLE RETURN";X$: PRINT 
90 D1 = 0:P1 = 0
100 Z = 0:P = 95:S = 2800:H = 3000:E = H - S
110 Y = 3:A = H / Y:I = 5:Q = 1
210 D = 0
215  PRINT : PRINT : PRINT "O' HAMURABI, PERMITI-ME RELATAR:";:Z = Z + 1
217  PRINT "NO ANO";Z; CHR$ (8);", MORRERAM DE FOME": PRINT D;"PESSOAS,";I;"IMIGRARAM,"
218 P = P + I
227  IF Q > 0 THEN 230
228 P =  INT (P / 2)
229  PRINT "UMA HORRIVEL PRAGA SE ABATEU": PRINT "SOBRE NOS! METADE DA POPULACAO": PRINT "MORREU."
230  PRINT "A POPULACAO AGORA E";P; CHR$ (8);"."
232  PRINT "A CIDADE POSSUI";A;"ACRES."
235  PRINT "COLHESTES";Y;"ALMUDES POR ACRE."
250  PRINT "OS RATOS COMERAM";E;"ALMUDES."
260  PRINT "TENDES AGORA";S;"ALMUDES EM": PRINT "ESTOQUE.": PRINT 
270  IF Z = 11 THEN 860
310 C =  INT (10 *  RND (1)):Y = C + 17
312  PRINT "O VALOR DA TERRA E";Y;"ALMUDES": PRINT "POR ACRE."
320  PRINT "QUANTOS ACRES COMPRAREIS";
321  INPUT Q: IF Q < 0 THEN 850
322  IF Y * Q <  = S THEN 330
323  GOSUB 710
324  GOTO 320
330  IF Q = 0 THEN 340
331 A = A + Q:S = S - Y * Q:C = 0
334  GOTO 400
340  PRINT "QUANTOS ACRES VENDEREIS";
341  INPUT Q: IF Q < 0 THEN 850
342  IF Q < A THEN 350
343  GOSUB 720
344  GOTO 340
350 A = A - Q:S = S + Y * Q:C = 0
400  PRINT 
410  PRINT "QUANTOS ALMUDES DESTINAREIS A": PRINT "ALIMENTACAO DO POVO";
411  INPUT Q
412  IF Q < 0 THEN 850
418  REM *** TENTANDO USAR MAIS GRAO DO QUE HA NOS SILOS?
420  IF Q <  = S THEN 430
421  GOSUB 710
422  GOTO 410
430 S = S - Q:C = 1: PRINT 
440  PRINT "QUANTOS ACRES PLANTAREIS";
441  INPUT D: IF D = 0 THEN 511
442  IF D < 0 THEN 850
444  REM *** TENTANDO PLANTAR MAIS ACRES DO QUE POSSUI?
445  IF D <  = A THEN 450
446  GOSUB 720
447  GOTO 440
449  REM *** HA GRAO SUFICIENTE PARA PLANTAR?
450  IF  INT (D / 2) <  = S THEN 455
452  GOSUB 710
453  GOTO 440
454  REM *** HA GENTE SUFICIENTE PARA A COLHEITA?
455  IF D < 10 * P THEN 510
460  PRINT "MAS TENDES APENAS";P;"PESSOAS": PRINT "PARA COLHER OS CAMPOS! PORTANTO,";
470  GOTO 440
510 S = S -  INT (D / 2)
511  GOSUB 800
512  REM *** UMA COLHEITA FARTA!
515 Y = C:H = D * Y:E = 0
521  GOSUB 800
522  IF  INT (C / 2) <  > C / 2 THEN 530
523  REM *** OS RATOS ESTAO A SOLTA!!
525 E =  INT (S / C)
530 S = S - E + H
531  GOSUB 800
532  REM *** VAMOS FAZER ALGUNS BEBES
533 I =  INT (C * (20 * A + S) / P / 100 + 1)
539  REM *** QUANTAS PESSOAS ESTAO DE BARRIGA CHEIA?
540 C =  INT (Q / 20)
541  REM *** 15% DE PROBABILIDADE DE PRAGA
542 Q =  INT (10 * (2 *  RND (1) - .3))
550  IF P < C THEN 210
551  REM *** MORREU DE FOME GENTE BASTANTE PARA UM IMPEACHMENT?
552 D = P - C: IF D > .45 * P THEN 560
553 P1 = ((Z - 1) * P1 + D * 100 / P) / Z
555 P = C:D1 = D1 + D: GOTO 215
560  PRINT : PRINT "DEIXASTES";D;"PESSOAS MORREREM": PRINT "DE FOME EM UM ANO!!!"
565  PRINT "DEVIDO A TAMANHA INCOMPETENCIA, NAO APENAS FOSTES DEPOSTO, COMO"
566  PRINT "TAMBEM VOS TORNASTES UM PARIA"
567  PRINT "PARA TODA A NACAO!!!!": GOTO 990
710  PRINT "O' HAMURABI, PENSAI DE NOVO."
711  PRINT "TENDES APENAS";S;"ALMUDES DE": PRINT "GRAO. PORTANTO,"
712  RETURN 
720  PRINT "O' HAMURABI, PENSAI DE NOVO.": PRINT "TENDES APENAS";A;"ACRES.": PRINT "PORTANTO,"
730  RETURN 
800 C =  INT ( RND (1) * 5) + 1
801  RETURN 
850  PRINT : PRINT "O' HAMURABI, NAO POSSO FAZER O  QUE DESEJAIS."
855  PRINT "PROCURAI OUTRO ADMINISTRADOR!!!"
857  GOTO 990
860  PRINT "EM VOSSO MANDATO DE DEZ ANOS,": PRINT P1;"POR CENTO DA POPULACAO"
862  PRINT "MORREU DE FOME POR ANO EM MEDIA,ISTO E, UM TOTAL DE";D1
865  PRINT "PESSOAS MORRERAM!!":L = A / P
870  PRINT "COMECASTES COM 10 ACRES POR": PRINT "PESSOA E TERMINASTES COM";L
875 "ACRES POR PESSOA.": PRINT 
880  IF P1 > 33 THEN 565
885  IF L < 7 THEN 565
890  IF P1 > 10 THEN 940
892  IF L < 9 THEN 940
895  IF P1 > 3 THEN 960
896  IF L < 10 THEN 960
900  PRINT "UM DESEMPENHO FANTASTICO!!!": PRINT "CARLOS MAGNO, DISRAELI E"
905  PRINT "JEFFERSON COMBINADOS NAO TERIAM FEITO MELHOR!": GOTO 990
940  PRINT "VOSSO DESEMPENHO DESAJEITADO": PRINT "TEM CHEIRO DE NERO E IVAN IV."
945  PRINT "O POVO (RESTANTE) VOS CONSIDERA UM GOVERNANTE DESPREZIVEL, E,"
950  PRINT "FRANCAMENTE, VOS ODEIA!!": GOTO 990
960  PRINT "VOSSO DESEMPENHO PODERIA TER": PRINT "SIDO UM POUCO MELHOR, MAS NA"
965  PRINT "VERDADE NAO FOI TAO MAL."; INT (P * .8 *  RND (1))
970  PRINT "PESSOAS ADORARIAM VER-VOS ASSAS-SINADO, MAS TODOS NOS TEMOS"
975  PRINT "ALGUNS PROBLEMAS."
990  PRINT : FOR N = 1 TO 10: PRINT  CHR$ (7);: NEXT N
995  PRINT "ATE MAIS VER.": PRINT 
999  END 
