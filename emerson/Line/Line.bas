0  HGR 
10  FOR M = 129 TO 159 STEP 2
11  POKE 245,M: CALL 54492
12  GOSUB 100
13 X0 = XM / 2:Y0 = YM / 2
20  FOR I = 1 TO 8
30 X1 =  RND (1) * XM
35 Y1 =  RND (1) * YM
50 C = ( RND (1) * (P2 - 1)) + 1
60  GOSUB 300
65 X0 = X1:Y0 = Y1
70  NEXT I
80  NEXT M
90  GOTO 10
100  REM CONFIGURA PARA O MODO GRAFICO ATUAL
110 VR = 32768: REM ENDERECO DA VRAM
120 MG =  PEEK (245) / 4 AND 7: REM MODO GRAFICO
130 BP = 2 - (MG AND 1): REM BITS POR PIXEL
140 P2 = 2 ^ BP: REM POTENCIA DE 2
150 MP = P2 - 1: REM MASCARA DE PIXEL
160 PB = 8 / BP: REM PIXELS POR BYTE
170 BL = 16 + (16 AND ((MG AND 6) > (5 AND (BP = 1)))): REM BYTES POR LINHA
180 XM = 64 + (64 AND (MG > 0)) + (128 AND (MG > 6)): REM X MAXIMO+1
190 YM = 64 + (32 AND (MG > 2)) + (96 AND (MG > 4)): REM Y MAXIMO+1
195  RETURN 
200  REM COLOR=C:PLOT X,Y
200  IF X < 0 OR X >  = XM OR Y < 0 OR Y >  = YM THEN  RETURN : REM PIXEL FORA DA TELA
210 EP = VR +  INT (Y) * BL +  INT (X / PB): REM ENDERECO DO PIXEL NA VRAM
220 SH = P2 ^ (PB - 1 - (X AND (PB - 1))): REM SHIFT DOS BITS
230  OUT 128, PEEK (245) AND 254: REM HABILITA VRAM
240 B0 =  PEEK (EP): REM BYTE ORIGINAL NA VRAM
245 B = B0 AND  NOT (MP * SH) OR (C AND MP) * SH: REM NOVO BYTE NA VRAM
246  POKE EP,B
250  OUT 128, PEEK (245) OR 1: REM DESABILITA VRAM
260  RETURN 
300  REM PLOT X0,Y0 TO X1,Y1
310 DX = X1 - X0:AX =  INT ( ABS (DX)):SX =  SGN (DX)
315 DY = Y1 - Y0:AY =  INT ( ABS (DY)):SY =  SGN (DY)
320  IF AY > AX THEN 350
330 Y = Y0:S =  INT (AX / 2): FOR X = X0 TO X1 STEP SX: GOSUB 200:S = S + AY: IF S >  = AX THEN S = S - AX:Y = Y + SY
340  NEXT X: RETURN 
350 X = X0:S =  INT (AY / 2): FOR Y = Y0 TO Y1 STEP SY: GOSUB 200:S = S + AX: IF S >  = AY THEN S = S - AY:X = X + SX
360  NEXT Y: RETURN 
