10  GOSUB 1000
20  HGR : OUT 128, PEEK (245) AND 254: REM HABILITA VRAM PARA ESCRITA
30 VR = 32768: REM ENDERECO DE INICIO DA VRAM
40  REM PREENCHE AS DUAS PRIMEIRAS LINHAS DA VRAM COM PADROES
50  FOR A = 0 TO 63
60 DV = 2: IF A AND 32 THEN DV = 8
70  POKE VR + A,((A / DV) AND 3) * 85
80  NEXT A
90  REM COPIA PARA O RESTO DA VRAM
100  FOR A = 64 TO 32 * 192 - 1
110  POKE VR + A, PEEK (VR + (A AND 63))
120  NEXT A
130  REM ALTERNA ENTRE MODOS DE VIDEO PARA SE AVALIAR O EFEITO
140  FOR A = 159 TO 153 STEP  - 2
150  OUT 128,A
160  POKE 284,0
170  IF  PEEK (284) = 0 THEN 170
180  NEXT A
190  GOTO 140
1000  REM INSTRUCOES
1010  TEXT : HOME
1020  PRINT "TESTE DE ";: INVERSE : PRINT "COLOR ARTIFACTING:": NORMAL
1030  PRINT "PRODUZ 15 PADROES VERTICAIS DE  PONTOS PARA TESTAR SE O TELEVI- SOR PRODUZ CORES 'FANTASMAS'."
1040  PRINT : PRINT "OS MODOS GRAFICOS TESTADOS SAO:"
1050  PRINT "256 X 192 PIXELS, 2 CORES:"
1060  INVERSE : PRINT "159";: NORMAL : PRINT " PRETO+BRANCO (HGR)"
1070  INVERSE : PRINT "157";: NORMAL : PRINT " PRETO+VERDE"
1080  PRINT "128 X 192 PIXELS, 4 CORES:"
1090  INVERSE : PRINT "155";: NORMAL : PRINT " BRANCO+CIANO+MAGENTA+LARANJA";
1100  INVERSE : PRINT "153";: NORMAL : PRINT " VERDE+AMARELO+AZUL+VERMELHO"
1110  PRINT : PRINT "APOS A GERACAO DOS PADROES, USE QUALQUER TECLA PARA ALTERNAR EN-TRE OS MODOS DE VIDEO.";
1120  POKE 284,0
1130  IF  PEEK (284) = 0 THEN 1130
1140  RETURN
[[code]]
----
Versão mais rápida, que usa código de máquina para produzir os padrões:
[[code]]
10  GOSUB 1000
20  GOSUB 2000
30  HGR
40  CALL 16128
50  REM  ALTERNA ENTRE MODOS DE VIDEO PARA SE AVALIAR O EFEITO
60  FOR A = 159 TO 153 STEP  - 2
70  OUT 128,A
80  POKE 284,0
90  IF  PEEK (284) = 0 THEN 90
100  NEXT A
110  GOTO 60
1000  REM INSTRUCOES
1010  TEXT : HOME
1020  PRINT "TESTE DE ";: INVERSE : PRINT "COLOR ARTIFACTING:": NORMAL
1030  PRINT "PRODUZ 15 PADROES VERTICAIS DE  PONTOS PARA TESTAR SE O TELEVI- SOR PRODUZ CORES 'FANTASMAS'."
1040  PRINT : PRINT "OS MODOS GRAFICOS TESTADOS SAO:"
1050  PRINT "256 X 192 PIXELS, 2 CORES:"
1060  INVERSE : PRINT "159";: NORMAL : PRINT " PRETO+BRANCO (HGR)"
1070  INVERSE : PRINT "157";: NORMAL : PRINT " PRETO+VERDE"
1080  PRINT "128 X 192 PIXELS, 4 CORES:"
1090  INVERSE : PRINT "155";: NORMAL : PRINT " BRANCO+CIANO+MAGENTA+LARANJA";
1100  INVERSE : PRINT "153";: NORMAL : PRINT " VERDE+AMARELO+AZUL+VERMELHO"
1110  PRINT : PRINT "APOS A GERACAO DOS PADROES, USE QUALQUER TECLA PARA ALTERNAR EN-TRE OS MODOS DE VIDEO.";
1120  POKE 284,0
1130  IF  PEEK (284) = 0 THEN 1130
1140  RETURN
2000  REM INSERE PROGRAMA EM LINGUAGEM DE MAQUINA
2010  FOR A = 0 TO 36
2020  READ B
2030  POKE 16128 + A, B
2040  NEXT A
2050  RETURN
2100  DATA 205,134,200,33,0,128,125,203
2110  DATA 111,40,2,15,15,15,230,3
2120  DATA 71,7,7,176,7,7,176,7
2130  DATA 7,176,119,35,124,254,152,32
2140  DATA 229,205,121,200,201
