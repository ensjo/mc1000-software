0  REM --------10--------20--------30--------40--------50--------60--------70
1  DATA 33,230,3,34,33,1
2  DATA 62,195,50,32,1,201,221,229,221,33,255,255,221,35,221,57
3  DATA 62,93,221,190,16,32,18,62,221,221,190,17,32,11,229,33
4  DATA 12,4,221,117,10,221,116,11,225,221,225,201,193,209,51,51
5  DATA 50,29,1,183,58,27,1,50,28,1,196,223,221,195,97,221
6  FOR I = 0 TO 69: READ B: POKE 986 + I,B: NEXT I
7  CALL 986
10 AT$ =  CHR$ (27) + "=": REM POSICIONAMENTO DE CURSOR
11 VR = 32768: REM ENDERECO DE INICIO DA VRAM
12 K0 = 283: REM ENDERECO DE KEY0 (INKEY$)
13 KU =  ASC ("P"):KD =  ASC ("L"):KL =  ASC ("Z"):KR =  ASC ("X"): REM TECLAS DE CONTROLE
14 TS =  ASC ("#"): REM SPRITE DO TANQUE
15 PS =  ASC ("O"): REM SPRITE DO PARAQUEDISTA
16 BS =  ASC (" "): REM SPRITE EM BRANCO
20 LT = 99999
30 HS = 0
40 S = 0
50 PO = 210
60  HOME
61  POKE 304,201: REM CURSOR SEM PISCAR
62  POKE 247,202: POKE 248,192: POKE 249,202: POKE 250,192: CALL 52013: REM CURSOR INVISIVEL
70  PRINT AT$; CHR$ (8); CHR$ (0 + 32);"--------------------------------"
80 T = 0
90 X =  INT ( RND (1) * 256)
110  IF X = PO THEN  GOTO 90
120  OUT 128,0: POKE VR + X,PS: POKE VR + PO,TS
130 LP = PO
135 T = T + 1
139  POKE K0,0
140  IF  PEEK (K0) = KL THEN PO = PO - 1: GOTO 190
150  IF  PEEK (K0) = KR THEN PO = PO + 1: GOTO 210
160  IF  PEEK (K0) = KU THEN PO = PO - 32: GOTO 220
170  IF  PEEK (K0) = KD THEN PO = PO + 32: GOTO 220
180  GOTO 135
190  IF (LP AND 31) = 0 THEN PO = LP
200  GOTO 220
210  IF (PO AND 31) = 0 THEN PO = LP
220  IF PO > 255 OR PO < 0 THEN PO = LP: GOTO 135
230  POKE VR + LP,BS
240  POKE VR + PO,TS
250 M =  INT ( RND (1) * 256)
270  IF M = PO THEN  POKE VR + PO,BS: PRINT AT$; CHR$ (4); CHR$ (1 + 32);"BOOM!!! VOCE ACERTOU UMA MINA!": GOTO 330
280  IF X = PO THEN S = S + 1: GOTO 90
290  IF S < 10 AND X = PO THEN 90
300  IF S = 10 THEN 330
310  GOTO 130
330  PRINT AT$; CHR$ (9); CHR$ (7 + 32);S;"PARAQUEDISTAS"
340  IF S = 10 THEN  PRINT AT$; CHR$ (10); CHR$ (7 + 32);"EM";T / 6;"SEGUNDOS"
350  IF S > HS THEN HS = S
360  IF S = 10 AND T < LT THEN LT = T
370  PRINT AT$; CHR$ (13); CHR$ (8 + 32);"RECORDE=";HS
380  PRINT AT$; CHR$ (14); CHR$ (0 + 32);"MENOR TEMPO=";LT / 6;"SEGUNDOS";
390  FOR F = 1 TO 1000: NEXT F
400  PRINT AT$; CHR$ (4); CHR$ (1 + 32); SPC( 30)
410  PRINT AT$; CHR$ (4); CHR$ (7 + 32);"OUTRA VEZ (S/N)?"
420  POKE K0,0
421  IF  PEEK (K0) = 0 THEN 421
422 K$ =  CHR$ ( PEEK (K0))
430  IF K$ = "S" THEN 40
440  IF K$ = "N" THEN  END
450  GOTO 420
