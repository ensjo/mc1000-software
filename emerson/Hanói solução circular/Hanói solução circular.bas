10  REM ## SOLUCAO NAO RECURSIVA PARA 'TORRES DE HANOI' ##
20  HOME
30  DEF  FN PP(P) = P + 1 - 3 *  ABS (P = 2): REM CALCULA O NUMERO DO PROXIMO PINO.
100  GOSUB 1000: REM INICIALIZA PINOS.
110  GOSUB 2000: REM EXIBE PINOS.
120  GOSUB 3000: REM RESOLVE.
999  END
1000  REM ## INICIALIZA PINOS ##
1010 Q = 10: REM QUANTIDADE DE DISCOS.
1020  DIM P(2,Q): REM CONTEUDO DOS PINOS 0,1,2.
1030  FOR I = 1 TO Q:P(0,I) = Q - I + 1:P(1,I) = 0:P(2,I) = 0: NEXT : REM INICIALIZA PINOS, SÃ“ O PINO 0 TEM DISCOS.
1040  DIM T(2): REM TOPOS DOS PINOS 0,1,2.
1050 T(0) = Q:T(1) = 0:T(2) = 0: REM INICIALIZA TOPOS DOS PINOS.
1060  RETURN
2000  REM ## EXIBE PINOS ##
2010  PRINT  CHR$ (30);: REM MOVE CURSOR PARA INICIO DA TELA.
2020  FOR I = Q TO 1 STEP  - 1
2030  FOR J = 0 TO 2
2040 D$ = "    ": IF P(J,I) THEN D$ =  STR$ (P(J,I))
2050  PRINT  TAB( 10 + J * 5);D$;
2060  NEXT J
2070  PRINT
2080  NEXT I
2090  RETURN
3000  REM ## RESOLVE ##
3010 O = 0: REM COMECAMOS COM PINO DE ORIGEM=0.
3020 D =  FN PP(O): REM PINO DE DESTINO=PINO SEGUINTE AO DE ORIGEM.
3030  IF T(D) = 0 THEN GOTO 3080: REM PINO DE DESTINO VAZIO: TRANSFERIR.
3040  IF P(D,T(D)) > P(O,T(O)) THEN GOTO 3080: REM DISCO NO PINO DE DESTINO E MAIOR: TRANSFERIR.
3050 D =  FN PP(D): REM IMPOSSIVEL TRANSFERIR: TENTAR OUTRO DISCO DE DESTINO.
3060  IF D = O THEN GOTO 3120: REM DEU A VOLTA SEM ACHAR UM PINO DE DESTINO VALIDO: TENTAR OUTRO PINO DE ORIGEM.
3070 GOTO 3030
3080 T(D) = T(D) + 1:P(D,T(D)) = P(O,T(O)):P(O,T(O)) = 0:T(O) = T(O) - 1: REM TRANSFERE DISCO.
3090  GOSUB 2000: REM EXIBE PINOS
3100  IF T(D) = Q THEN  RETURN : REM PINO DE DESTINO ESTA CHEIO=SOLUCAO ENCONTRADA: TERMINAR.
3110 O = D: REM PINO DE ORIGEM=PINO DE DESTINO.
3120 O =  FN PP(O): REM AVANCA O PINO DE ORIGEM.
3130  IF T(O) = 0 THEN 3120: REM PINO DE ORIGEM VAZIO: TENTAR OUTRO PINO DE ORIGEM.
3140  GOTO 3020: REM TENTA TRANSFERIR.
