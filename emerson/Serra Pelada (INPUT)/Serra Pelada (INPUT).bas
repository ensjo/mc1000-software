20  DIM A(1,5),C(1,4),A$(1)
120  TEXT : HOME : PRINT "QUANTOS JOGADORES (1 OU 2)?";
125  POKE 283,0
130 A$ =  CHR$ ( PEEK (283)): IF A$ < "1" OR A$ > "2" THEN 130
140 P =  VAL (A$):NO = P:ER = 1E4:A(0,0) = 2E6:A(0,1) = A(0,0):A(1,0) = A(0,0):A(1,1) = A(0,0):A(0,3) = 1E4:A(1,3) = A(0,3)
150  FOR N = 1 TO P: PRINT : PRINT "NOME DO JOGADOR";N;: INPUT A$(N - 1): IF  LEN (A$(N - 1)) > 8 THEN A$(N - 1) =  LEFT$ (A$(N - 1),8)
160  NEXT
200  FOR N = 0 TO 29: FOR M = 0 TO NO - 1
202  HOME
210  PRINT  TAB( 8);: INVERSE : PRINT " MINA DE OURO ": NORMAL
220  PRINT  TAB( 12);A$(0);: IF NO = 2 THEN  PRINT  TAB( 22);A$(1);
230  PRINT : PRINT "SALDO TOTAL"; TAB( 12);A(0,0);: IF NO = 2 THEN  PRINT  TAB( 22);A(1,0);
240  PRINT : PRINT "DINHEIRO"; TAB( 12);A(0,1);: IF NO = 2 THEN  PRINT  TAB( 22);A(1,1);
250  PRINT : PRINT "OURO   KG"; TAB( 12);A(0,2);: IF NO = 2 THEN  PRINT  TAB( 22);A(1,2);
260  PRINT : PRINT "P/ CAVAR $"; TAB( 12);A(0,3);: IF NO = 2 THEN  PRINT  TAB( 22);A(1,3);
270  PRINT : PRINT "QTD. MINAS"; TAB( 12);A(0,4);: IF NO = 2 THEN  PRINT  TAB( 22);A(1,4);
280  PRINT : PRINT "PROFUND. M"; TAB( 12);A(0,5);: IF NO = 2 THEN  PRINT  TAB( 22);A(1,5);
300  PRINT : PRINT "COTACAO DO OURO: $";ER;"/KG"
400  PRINT  TAB( 10);: INVERSE : PRINT A$(M): NORMAL
500  PRINT "1) PESQUISA E DESENVOLVIMENTO"
510  PRINT "2) LEVANTAMENTO GEOLOGICO"
520  PRINT "3) CAVAR MAIS 200 M"
530  PRINT "4) VENDER OURO NO MERCADO"
540  PRINT "5) PASSAR A VEZ"
595  POKE 283,0
600 A$ =  CHR$ ( PEEK (283)): IF A$ < "1" OR A$ > "5" THEN 600
610  PRINT
620  ON  VAL (A$) GOSUB 1000,2000,3000,4000,5000
700  IF A(M,1) < 0 THEN 7000
710 ER = ER +  INT ( RND (1) * 1000) - 500
720  IF  INT ( RND (1) * 1600) - A(M,2) < 0 THEN  GOSUB 900
740 A(M,0) = A(M,1) + A(M,2) * ER
750  HOME
790  NEXT M,N
810  HOME
820  PRINT  TAB( 9);: INVERSE : PRINT " FIM DE JOGO ": NORMAL
830  PRINT : PRINT "SALDO DOTAL DE ";A$(0);":";A(0,0)
840  IF NO = 2 THEN  PRINT : PRINT "SALDO TOTAL DE ";A$(1);":";A(1,0)
850  PRINT : PRINT : PRINT "QUALQUER TECLA PARA RECOMECAR"
855  POKE 283,0
860  IF  PEEK (283) = 0 THEN 860
865  RUN
900  HOME
905 JK =  INT ( RND (1) * 100) + 49: IF JK > A(M,2) THEN JK = A(M,2)
910  PRINT  TAB( 10);: INVERSE : PRINT " R O U B O ": NORMAL
920  PRINT : PRINT "FORAM ROUBADOS";JK;"KG DE SEU OURO":A(M,2) = A(M,2) - JK:A(M,0) = A(M,0) - JK * ER
930  FOR I = 1 TO 2000: NEXT
940  HOME : RETURN
1000  HOME
1010  PRINT  TAB( 2);: INVERSE : PRINT " PESQUISA E DESENVOLVIMENTO ": NORMAL : PRINT  TAB( 6);"PARA REDUZIR CUSTOS"
1020  PRINT : INPUT "QUANTO PRETENDE INVESTIR";RD
1030  IF RD < 0 THEN 1000
1050 A(M,3) = A(M,3) -  INT (RD / 20) - 1
1060  IF A(M,3) < 0 THEN A(M,3) = 0
1080 A(M,1) = A(M,1) - RD:A(M,0) = A(M,0) - RD
1100  PRINT : PRINT "OS CUSTOS DE MINERACAO FORAM": PRINT "REDUZIDOS EM $"; INT (RD / 20) + 1;"POR 200 M"
1110  FOR Z = 1 TO 2000: NEXT
1120  RETURN
2000  HOME
2030 R(M) = 0:C(M,0) =  INT ( RND (1) * 90) + 10:C(M,1) = 200 *  INT ( RND (1) * 5 + 1):C(M,2) =  INT ( RND (1) * 200 + 1):LL =  INT ( RND (1) * 3) - 1
2050 C(M,3) = C(M,1) + LL * 200
2070 C(M,4) = 0:KK =  INT ( RND (1) * 100): IF KK < C(M,0) THEN C(M,4) = 1
2080  PRINT  TAB( 4);: INVERSE : PRINT " LEVANTAMENTO GEOLOGICO ": NORMAL : PRINT : PRINT "PROBABILIDADE DE ENCONTRAR OURO:";C(M,0);"%"
2090  PRINT : PRINT "PROVAVEL PROFUNDIDADE:";C(M,1);"M": PRINT : PRINT "PROVAVEL QUANTIDADE:";C(M,2);"KG"
2100 Z =  INT ( RND (1) * 150000):A(M,1) = A(M,1) - Z:A(M,0) = A(M,0) - Z
2110  PRINT : PRINT "COMECAR AS ESCAVACOES (S/N)?"
2115  POKE 283,0
2120  IF  PEEK (283) = 0 THEN 2120
2130  IF  CHR$ ( PEEK (283)) = "S" THEN A(M,5) = 0:R(M) = 1: GOTO 3000
2500  RETURN
3000  HOME
3010  IF R(M) = 0 THEN  PRINT "A MINA NAO FOI ABERTA!": FOR Z = 1 TO 1000: NEXT : RETURN
3020  HGR : PLOT 0,50 TO 255,50
3030  PLOT 125,45 TO 130,45 TO 130,40 TO 125,40 TO 125,45: PLOT 145,45 TO 150,45 TO 150,40 TO 145,40 TO 145,45: PLOT 135,50 TO 135,40 TO 140,40 TO 140,50
3040  PLOT 120,50 TO 120,40 TO 130,30 TO 135,30 TO 135,25 TO 140,25 TO 140,30 TO 150,30 TO 150,5 TO 175,5 TO 175,35 TO 185,45 TO 185,50 TO 210,50 TO 175,5
3050  FOR I = 58 TO 151 STEP 8: PLOT 0,I TO 100,I: NEXT
3090 A(M,1) = A(M,1) - A(M,3):A(M,0) = A(M,0) - A(M,4):A(M,5) = A(M,5) + 200
3100  FOR I = 5 TO (A(M,5) * 8 / 100) + 50: PLOT 155 + 4 *  RND (1),I TO 170 - 4 *  RND (1),I: NEXT
3115  FOR Z = 1 TO 500: NEXT : TEXT : HOME
3120  IF A(M,5) = C(M,3) AND C(M,4) = 1 THEN 3500
3130  PRINT "NADA DE OURO AINDA!": IF A(M,5) = C(M,1) + 200 THEN  PRINT "NAO HA OURO NESTA MINA": PRINT "VA CAVAR EM OUTRO LUGAR":A(M,5) = 0:R(M) = 0
3140  FOR Z = 1 TO 1500: NEXT
3300  HOME : RETURN
3500  PRINT  TAB( 9);"O U R O !!!!"
3550 A(M,4) = A(M,4) + 1:A(M,2) = A(M,2) + C(M,2):A(M,0) = A(M,0) + A(M,2) * ER:A(M,5) = A(M,5) = 0:R(M) = 0: GOTO 3140
4000  HOME
4010  PRINT  TAB( 3);: INVERSE : PRINT " CAIXA ECONOMICA FEDERAL ": NORMAL : PRINT  TAB( 3);: INVERSE : PRINT " AGENCIA DE SERRA PELADA ": NORMAL
4020  PRINT : PRINT "COTACAO DO OURO: $";ER;"/KG": PRINT : INPUT "QUANTOS KG VAI VENDER";NT
4080 NT =  INT (NT)
4090  IF NT > A(M,2) OR NT < 0 THEN 4000
4100 A(M,2) = A(M,2) - NT:A(M,1) = A(M,1) + NT * ER:A(M,0) = A(M,0) + NT * ER
4130  PRINT : PRINT NT;"KG VENDIDOS POR";NT * ER: FOR Z = 1 TO 1000: NEXT : RETURN
5000  RETURN
7000  HOME
7010  PRINT  TAB( 3);: INVERSE : PRINT " JORNAL DE SERRA PELADA ": NORMAL : PRINT  TAB( 4);"FALENCIAS E CONCORDATAS"
7020  PRINT : PRINT "FALIU HOJE A EMPRESA DE MINERA- CAO ";A$(M);" LTDA. GRACAS A": PRINT "MA ADMINISTRACAO DE SEU PROPRIE-TARIO."
7030  PRINT : PRINT "QUALQUER TECLA PARA JOGAR NOVA- MENTE.";
7035  POKE 283,0
7040  IF  PEEK (283) = 0 THEN 7040
7045  RUN
