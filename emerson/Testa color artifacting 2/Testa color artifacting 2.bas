0  REM DESENHA 16X16=256 PADROES DE 2X2 CORES NAS TELAS DE ALTA RESOLUCAO
10  HGR :M = 153: OUT 128,M AND 254: REM ENTRA EM MODO GRAFICO E HABILITA VRAM PARA ESCRITA.
20 E = 32768: REM ENDERECO DO INICIO DA VRAM
30  FOR C0 = 0 TO 3: REM COR SUPERIOR ESQUERDA
40  FOR C1 = 0 TO 3: REM COR SUPERIOR DIREITA
50 B0 = (C0 * 4 + C1) * 17: REM BYTE SUPERIOR DO PADRAO
60  FOR I = 0 TO 5: REM CADA PADRAO TERA 6X2=12 LINHAS DE ALTURA
70  FOR E = E TO E + 31: POKE E,B0: NEXT E: REM DESENHA BYTE SUPERIOR DO PADRAO (E O MESMO PARA TODOS OS PADROES DA LINHA)
80  FOR C2 = 0 TO 3: REM COR INFERIOR ESQUERDA
90  FOR C3 = 0 TO 3: REM COR INFERIOR DIREITA
100 B1 = (C2 * 4 + C3) * 17: REM BYTE INFERIOR DO PADRAO
110  FOR E = E TO E + 1: POKE E,B1: NEXT E: REM DESENHA BYTE INFERIOR DO PADRAO
120  NEXT C3,C2,I,C1,C0
130  REM ALTERNA MODO DE VIDEO COM TECLAS 1,2,3,4; TECLA 0 TERMINA PROGRAMA
140  OUT 128,M
150  POKE 284,0
160 T$ =  CHR$ ( PEEK (284))
170  IF T$ >  = "1" AND T$ <  = "4" THEN M = 150 +  VAL (T$) * 2: GOTO 140
180  IF T$ = "0" THEN  TEXT : END
190  GOTO 160
