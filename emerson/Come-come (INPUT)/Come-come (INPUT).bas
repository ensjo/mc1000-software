0  REM --------10--------20--------30--------40--------50--------60--------70
1  DATA 33,230,3,34,33,1
2  DATA 62,195,50,32,1,201,221,229,221,33,255,255,221,35,221,57
3  DATA 62,93,221,190,16,32,18,62,221,221,190,17,32,11,229,33
4  DATA 12,4,221,117,10,221,116,11,225,221,225,201,193,209,51,51
5  DATA 50,29,1,183,58,27,1,50,28,1,196,223,221,195,97,221
6  FOR I = 0 TO 69: READ B: POKE 986 + I,B: NEXT I
7  CALL 986
10 VRAM = 32768: REM ENDERECO DE INICIO DA VRAM
11 AT$ =  CHR$ (27) + "=": REM POSICIONAMENTO DE CURSOR
20 FA = 999999
30  HOME
31  POKE 304,201: REM CURSOR SEM PISCAR
32  POKE 247,202: POKE 248,192: POKE 249,202: POKE 250,192: CALL 52013: REM CURSOR INVISIVEL
40 P = 297
50  RESTORE 2000
60 D = 0
170 T = 0
1000  FOR N = 0 TO 15
1010  READ A$
1020  PRINT AT$; CHR$ (N); CHR$ (32 + 6);A$;: POKE 910,0: OUT 128,0
1030  FOR M = 0 TO 18
1040 E = VRAM + N * 32 + 6 + M: IF  PEEK (E) =  ASC ("A") THEN  POKE E,128 +  ASC (" ")
1050  NEXT M,N
1080  POKE VRAM + P, ASC ("*")
1090 LP = P
1095 T = T + 1:K$ = "": IF  PEEK (285) THEN K$ =  CHR$ ( PEEK (284))
1100  IF K$ = "Z" AND  PEEK (VRAM + P - 1) < 128 THEN P = P - 1
1110  IF K$ = "X" AND  PEEK (VRAM + P + 1) < 128 THEN P = P + 1
1120  IF K$ = "P" AND  PEEK (VRAM + P - 32) < 128 THEN P = P - 32
1130  IF K$ = "L" AND  PEEK (VRAM + P + 32) < 128 THEN P = P + 32
1140  IF  PEEK (VRAM + P) =  ASC (".") THEN D = D + 1
1150  POKE VRAM + LP, ASC (" ")
1160  IF D = 153 THEN 1180
1170  GOTO 1080
1180  POKE VRAM + P, ASC (" ")
1190 TI =  INT (T / 10)
1200  PRINT AT$; CHR$ (0); CHR$ (25 + 32);"TEMPO:";
1210  PRINT AT$; CHR$ (1); CHR$ (25 + 32);TI;
1220  IF FA > TI THEN FA = TI
1225  PRINT AT$; CHR$ (4); CHR$ (25 + 32);"MELHOR";
1230  PRINT AT$; CHR$ (5); CHR$ (25 + 32);"TEMPO:";
1240  PRINT AT$; CHR$ (6); CHR$ (25 + 32);FA;
1250  PRINT AT$; CHR$ (9); CHR$ (25 + 32);"PRESSI-";
1255  PRINT AT$; CHR$ (10); CHR$ (25 + 32);"  ONE";
1260  PRINT AT$; CHR$ (11); CHR$ (25 + 32);"<ENTER>";
1270  PRINT AT$; CHR$ (12); CHR$ (25 + 32);" PARA";
1275  PRINT AT$; CHR$ (13); CHR$ (25 + 32);"COMECAR";
1280  PRINT AT$; CHR$ (14); CHR$ (25 + 32);"DE NOVO";: POKE 910,0
1290  IF  PEEK (285) THEN  IF  PEEK (284) = 13 THEN 1310
1300  GOTO 1290
1310  PRINT AT$; CHR$ (9); CHR$ (25 + 32);"       ";
1320  PRINT AT$; CHR$ (10); CHR$ (25 + 32);"       ";
1330  PRINT AT$; CHR$ (11); CHR$ (25 + 32);"       ";
1340  PRINT AT$; CHR$ (12); CHR$ (25 + 32);"       ";
1350  PRINT AT$; CHR$ (13); CHR$ (25 + 32);"       ";
1360  PRINT AT$; CHR$ (14); CHR$ (25 + 32);"       ";: POKE 910,0
1370  GOTO 40
2000  DATA AAAAAAAAAAAAAAAAAAA
2010  DATA A.......AAA.......A
2020  DATA A.AA.AA.....AA.AA.A
2030  DATA A.A.....AAA.....A.A
2040  DATA A...A.A.....A.A...A
2050  DATA A.AAA.AA.A.AA.AAA.A
2060  DATA A...A.........A...A
2070  DATA AAA...A.AAA.A...AAA
2080  DATA AAA...A.AAA.A...AAA
2090  DATA A...A.........A...A
2100  DATA A.AAA.AA.A.AA.AAA.A
2110  DATA A...A.A.....A.A...A
2120  DATA A.A.....AAA.....A.A
2130  DATA A.AA.AA.....AA.AA.A
2140  DATA A.......AAA.......A
2150  DATA AAAAAAAAAAAAAAAAAAA
