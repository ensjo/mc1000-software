1  DATA ""
2  DATA ""
3  DATA ""
4  DATA ""
5  DATA ""
6  DATA ""
10  DATA "RRRRRRRRRRXXXXXXXXXXRRRRRRRRRRXXXXXXXXXXXXXXXXXX"
20  DATA "RRRRRRRRRRRRRRRRRRRRRRRRRRRRRRX-----------------X"
30  DATA "XXXXXXXXXXRRRRRRRRRRXXXXXXXXXX---=============---X"
40  DATA "OOOOOOOOOOXXXXXXXXXXOOOOOOOOOX--======*==*=====--X"
50  DATA "OOOOOOOOOOOOOOOOOOOOOOOOOOOOOX-==*==============-X"
60  DATA "XXXXXXXXXXOOOOOOOOOOXXXXXXXXXX-==========XX==*==-X XX"
70  DATA "YYYYYYYYYYXXXXXXXXXXYYYYYYYYYX-=========X++X====-XX++X"
80  DATA "YYYYYYYYYYYYYYYYYYYYYYYXXXXYYX-======*==X+++X===-X+++X"
90  DATA "XXXXXXXXXXYYYYYYYYYYXXXX++XXYX-=========X++++XXXX++++X"
100  DATA "GGGGGGGGGGXXXXXXXXXXGGGXX++XXX-===*=====X++++++++++++X"
110  DATA "GGGGGGGGGGGGGGGGGGGGGGGGXX++XX-=======*X++++++++++++++X"
120  DATA "XXXXXXXXXXGGGGGGGGGGXXXXXXX++X-=*======X+++.X+++++.X++X"
130  DATA "BBBBBBBBBBXXXXXXXXXXBBBBBBXXXX-========X+++XX+++X+XX++X"
140  DATA "BBBBBBBBBBBBBBBBBBBBBBBBBBBBXX-=====*==X+==+++++++++==X"
150  DATA "XXXXXXXXXXBBBBBBBBBBXXXXXXXXXX--=*=====X+==+X++X++X+==X"
160  DATA "MMMMMMMMMMXXXXXXXXXXMMMMMMMMMX---=======X+++XXXXXXX++X"
170  DATA "MMMMMMMMMMMMMMMMMMMMMMMMMMMMXXX----------X++++++++++X"
180  DATA "XXXXXXXXXXMMMMMMMMMMXXXXXXXX+++XXXXXXXXXXXXXXXXXXXXX"
190  DATA "                           X++XX X++X     X++X X++X"
200  DATA "                           XXXX  XXX       XXX  XX"
210  DATA "END"
1000  GR : OUT 128,64: REM SET SEMIGRAPHICS 4 MODE, VRAM FILLED WITH ZEROES
1010 V = 32768: REM VRAM ADDRESS FOR POKE-ING
1020 ED = 0: REM END-OF-DATA FLAG
1030  FOR Y = 0 TO 15: REM FOR EVERY LINE ON THE SCREEN...
1040  FOR CY = 0 TO 1: REM FOR EVERY VERTICAL PIXEL OF THE CHARACTER...
1050  IF  NOT ED THEN  READ L$: IF L$ = "END" THEN ED =  - 1:L$ = "": REM READ LINE STRING, IF ANY
1060  FOR X = 0 TO 31: REM FOR EVERY COLUMN ON THE SCREEN...
1070  FOR CX = 0 TO 1: REM FOR EVERY HORIZONTAL PIXEL OF THE CHARACTER...
1080 LX = 1 + X + X + CX:C$ = " ": IF LX <  =  LEN (L$) THEN C$ =  MID$ (L$,LX,1): REM GET ONE CHARACTER FROM THE LINE STRING TO BE MAPPED TO PIXEL
1090 CL =  - 1: REM NO PIXEL (BLACK) FOR UNMAPPED CHARACTERS
1100  IF C$ = "G" THEN CL = 0: REM GREEN
1110  IF C$ = "Y" THEN CL = 1: REM YELLOW
1120  IF C$ = "B" OR C$ = " " THEN CL = 2: REM BLUE
1130  IF C$ = "R" THEN CL = 3: REM RED
1140  IF C$ = "W" OR C$ = "." THEN CL = 4: REM WHITE
1150  IF C$ = "C" OR C$ = "+" THEN CL = 5: REM CYAN
1160  IF C$ = "M" OR C$ = "=" THEN CL = 6: REM MAGENTA
1170  IF C$ = "O" OR C$ = "-" THEN CL = 7: REM ORANGE
1180  IF CL <  >  - 1 THEN  POKE V + 32 * Y + X, PEEK (V + 32 * Y + X) AND 15 OR (CL * 16) OR 2 ^ (2 - (CY + CY) + 1 - CX): REM PLOT PIXEL IF NOT BLACK
1190  NEXT CX,X,CY,Y
1200  GOTO 1200
