10  GOSUB 1000
20  GOSUB 2000
30  GOSUB 3000
40  POKE 284,0
50  GOSUB 4000
60 X$ =  CHR$ ( PEEK (284))
70  IF X$ = "," THEN  POKE 245,( PEEK (245) - 2) AND 255: GOTO 40
80  IF X$ = "." THEN  POKE 245,( PEEK (245) + 2) AND 255: GOTO 40
90  IF X$ = " " THEN  TEXT : END
100  GOTO 60
1000  REM INSTRUCOES
1010  TEXT : HOME
1020  PRINT "ESTE PROGRAMA PERMITE PERCORRER TODOS OS MODOS DE VIDEO DO      MC-1000."
1030  PRINT : PRINT "TECLAS DE CONTROLE:"
1040  PRINT "VIRGULA ";: INVERSE : PRINT "[,]";: NORMAL : PRINT ": MODO ANTERIOR."
1050  PRINT "PONTO   ";: INVERSE : PRINT "[.]";: NORMAL : PRINT ": MODO SEGUINTE."
1060  PRINT "    ";: INVERSE : PRINT "[SPACE]";: NORMAL : PRINT ": SAI DO PROGRAMA."
1070  RETURN
2000  REM INICIALIZACAO
2010  PRINT : PRINT "AGUARDE..."
2020 VR = 32768: REM INICIO DA VRAM
2030  REM PREENCHE MATRIZES COM FORMATOS DOS ALGARISMOS
2040  DATA 119,36,93,109,46,107,123,37,127,111
2050  DIM D(9,3,6): REM 10 ALGARISMOS COM MATRIZ 4X7
2060  FOR I = 0 TO 9
2065  PRINT I * 10;"%"; CHR$ (11)
2070  READ X
2080  FOR J = 0 TO 6
2090  IF (X AND 2 ^ J) = 0 THEN 2170
2100 PX = 3 AND (J = 2 OR J = 5)
2110 PY = 3 AND (J = 3 OR J = 4 OR J = 5) OR 6 AND (J = 6)
2120 DX = 1 AND (J = 0 OR J = 3 OR J = 6)
2130 DY = 1 - DX
2140  FOR K = 0 TO 3
2150 D(I,PX + K * DX,PY + K * DY) = 255
2160  NEXT K
2170  NEXT J,I
2180  PRINT "     "; CHR$ (11); CHR$ (13);"PRESSIONE QUALQUER TECLA."
2190  POKE 284,0
2200  IF  PEEK (284) = 0 THEN 2200
2210  RETURN
3000  REM PREENCHE A TELA COM PADROES
3010  HGR
3020  OUT 128, PEEK (245) AND 254
3030  REM TODOS OS BYTES POSSIVEIS
3040  FOR I = 0 TO 255
3050  POKE VR + I,I
3060  NEXT I
3070  REM LISTRAS VERTICAIS
3080  FOR I = 1 TO 12
3090  FOR J = 9 TO I * 16 - 1
3100  POKE VR + 15 + I + J * 32,10
3200  NEXT J,I
3300  RETURN
4000  REM EXIBE O NUMERO DO MODO DE VIDEO ATUAL
4010 MX =  PEEK (245)
4020  OUT 128,MX AND 254
4030  FOR I = 0 TO 2: REM CASA DAS UNIDADES, DEZENAS E CENTENAS
4040 VD = VR + 256 + 32 + 5 * (2 - I)
4050 M0 =  INT (MX / 10)
4060 D = MX - 10 * M0: REM ALGARISMO DA CASA ATUAL
4070  FOR J = 0 TO 6: REM DESENHA O ALGARISMO
4080 VY = VD + 32 * J
4090  FOR K = 0 TO 3
4100  POKE VY + K,D(D,K,J)
4110  NEXT K,J
4120 MX = M0
4130 X$ =  CHR$ ( PEEK (284)): IF X$ = "," OR X$ = "." OR X$ = " " THEN I = 2
4140  NEXT I
4150  RETURN
