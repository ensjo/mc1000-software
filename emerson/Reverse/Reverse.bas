10  REM ** REVERSE **
20  REM VERSAO PARA MC-1000
30  REM EMERSON JOSE S DA COSTA
40  REM 2013-10-30
100  REM TELA INICIAL
110  TEXT : HOME 
120  INVERSE : PRINT  TAB( 12);"REVERSE"; TAB( 32): NORMAL 
200  REM INICIALIZA VARIAVEIS
210 N = 9: DIM A(N)
220 T = 0
300  REM CRIA LISTA DE NUMEROS ALEATORIA
310  FOR X = 1 TO N
320 I =  INT ( RND (1) * N) + 1: IF A(I) THEN  GOTO 320
330 A(I) = X
340  NEXT X
360  REM NAO PODE ESTAR RESOLVIDO LOGO AO INICIAR
370  GOSUB 1000: IF R THEN  CLEAR : GOTO 200
400  REM EXIBE SITUACAO ATUAL
410  PRINT : PRINT "A LISTA DE NUMEROS E'"
420  FOR I = 1 TO N: PRINT  STR$ (A(I));: NEXT I
430  PRINT 
500  REM VERIFICA SE RESOLVEU O PROBLEMA
510  GOSUB 1000: IF  NOT (R) THEN  GOTO 600
520  PRINT : PRINT "O PROBLEMA FOI RESOLVIDO EM": PRINT T;"TURNOS!"
530  END 
600  REM INTERACAO COM O JOGADOR
610 T = T + 1: PRINT : PRINT "TURNO:";T
620  INPUT "QUANTOS NUMEROS INVERTER";X
630 X =  INT (X): IF X < 1 OR X > N THEN  PRINT "VALOR INVALIDO!": GOTO 620
700  REM REVERTE X NUMEROS
710 J = X: FOR I = 1 TO  INT (X / 2)
720 A = A(I):A(I) = A(J):A(J) = A
730 J = J - 1: NEXT I
800  GOTO 400
1000  REM VERIFICA SE O PROBLEMA FOI RESOLVIDO
1010  FOR I = 1 TO N
1020  IF A(I) <  > I THEN R = 0: RETURN 
1030  NEXT I
1040 R =  - 1: RETURN 
