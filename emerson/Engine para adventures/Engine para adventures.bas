0 TEXT : HOME : NORMAL :AT$ = CHR$ (27) + "=":MU = - 1: DIM OL(9): GOTO 1000: REM ENGINE PARA ADVENTURES - EMERSON JOSE SILVEIRA DA COSTA (ENSJO) - 2012-08-07.
1 PRINT AT$; CHR$ (Y); CHR$ (X + 32);
2 POKE 910,0: RETURN
3 MO$ = MO$ + LEFT$ (OP$,1)
4 GOSUB 1: PRINT OP$;: RETURN
5 CALL 49158:I = PEEK (284): IF I = 3 THEN STOP
6 T$ = CHR$ (I):T = VAL (T$): FOR I = 1 TO LEN (MO$): IF T$ = MID$ (MO$,I,1) THEN RETURN
7 NEXT : GOTO 5
8 Y = 15:X = 24: GOSUB 1: PRINT "RETURN:";: CALL 49158: RETURN
9 TT = 1
10 IX = 0
20 OI = 0
30 GOSUB 200: IF T$ = "." GOTO 30
40 ON TT GOTO 50,80,110: STOP
50 IF T$ = "I" OR T$ = "V" THEN IX = T$ = "I": GOTO 20
60 IF T THEN OX = OL(T):TT = 2: GOTO 10
70 C$ = T$: RETURN
80 IF T$ = "@" THEN TT = 1: GOTO 10
90 C$ = "O" + T$: IF T$ = "U" THEN TT = 3: GOTO 10
100 RETURN
110 IF T$ = "@" THEN TT = 2: GOTO 10
120 IF T$ = "I" OR T$ = "V" THEN IX = T$ = "I": GOTO 20
130 OY = OL(T): RETURN
200 HOME : INVERSE : GOSUB 2: PRINT S$(SX); TAB( 32);: IF TT = 2 OR TT = 3 THEN PRINT O$(OX); TAB( 64);: IF TT = 3 THEN PRINT "USAR EM"; TAB( 96);
210 NORMAL : GOSUB 2:MO$ = ""
220 IF TT = 0 THEN RETURN
230 ON TT GOSUB 300,410,360
240 IF TT = 2 OR TT = 3 THEN Y = 15:X = 12:OP$ = "@=CANCELAR": GOSUB 3
250 IF TT = 1 OR TT = 3 THEN GOSUB 500
260 Y = 15:X = 25: GOSUB 1: PRINT "OPCAO:";: GOTO 5
300 Y = 1: IF SN(SX) THEN X = 0:OP$ = "N=NORTE": GOSUB 3
310 IF SS(SX) THEN X = 8:OP$ = "S=SUL": GOSUB 3
320 IF SL(SX) THEN X = 16:OP$ = "L=LESTE": GOSUB 3
330 IF SO(SX) THEN X = 24:OP$ = "O=OESTE": GOSUB 3
340 Y = 2: IF SC(SX) THEN X = 0:OP$ = "C=CIMA": GOSUB 3
350 IF SB(SX) THEN X = 8:C$ = "OP=BAIXO": GOSUB 3
360 Y = 3:X = 0: IF NOT IX THEN OP$ = "I=INVENTARIO": GOSUB 3
370 IF IX THEN OP$ = "V=COISAS A VISTA": GOSUB 3
380 Y = 5:X = 0: GOSUB 1: IF NOT IX THEN PRINT "COISAS A VISTA:";
390 IF IX THEN PRINT "INVENTARIO:";
400 RETURN
410 IX = O(OX) = MU:Y = 2:X = 0:OP$ = "E=EXAMINAR": GOSUB 3:X = 12:OP$ = "A=ACIONAR": GOSUB 3
420 X = 24: IF NOT IX THEN OP$ = "P=PEGAR": GOSUB 3
430 IF IX THEN OP$ = "L=LARGAR": GOSUB 3
440 Y = 3:X = 0:OP$ = "U=USAR EM": GOSUB 3
450 IF NOT IX THEN X = 12:OP$ = "I=IR PARA": GOSUB 3
460 RETURN
500 FOR I = 1 TO 9:OL(I) = MU: NEXT :I = 0: IF OI > OQ THEN OI = 0
510 LP = OI < > 0
520 IF OI > OQ THEN 560
530 IF NOT (( NOT IX AND O(OI) = SX) OR (IX AND O(OI) = MU)) THEN OI = OI + 1: GOTO 520
540 I = I + 1: IF I = 10 THEN LP = MU: GOTO 560
550 OL(I) = OI:Y = 5 + I:X = 0:OP$ = CHR$ (48 + I): GOSUB 3: PRINT "=";O$(OI);:OI = OI + 1: GOTO 520
560 IF LP THEN Y = 15:X = 0:OP$ = ".=+ITENS": GOSUB 3
570 RETURN
1000 REM AVENTURA INPUT ADAPTADA
1010 GOSUB 50000: REM INICIALIZACAO
1020 GOSUB 10000: REM CHECAGENS ANTERIORES A EXIBICAO DA SALA
1030 GOSUB 9: REM (ENGINE DE INTERFACE) EXIBE SALA E AGUARDA COMANDO
1040 GOSUB 20000: REM INTERPRETA COMANDO
1050 GOSUB 30000 REM CHECAGENS POSTERIORES A EXECUCAO DO COMANDO
1060 GOTO 1020
10000 REM CHECAGENS ANTERIORES A EXIBICAO DA SALA
10010 IF SX = 8 THEN IF NOT (O(8) = MU OR O(8) = SX) THEN SX = 9
10020 IF SX = 9 THEN IF (O(8) = MU OR O(8) = SX) THEN SX = 8
10030 IF NOT CI THEN IF RND (1) < .07 THEN O(9) = SX:CI = MU
10040 RETURN
20000 REM INTERPRETA COMANDO
20010 IF C$ = "N" THEN SX = SN(SX): RETURN
20020 IF C$ = "S" THEN SX = SS(SX): RETURN
20030 IF C$ = "L" THEN SX = SL(SX): RETURN
20040 IF C$ = "O" THEN SX = SO(SX): RETURN
20050 IF C$ = "C" THEN SX = SC(SX): RETURN
20060 IF C$ = "B" THEN SX = SB(SX): RETURN
20070 IF C$ = "OE" GOTO 21000
20080 IF C$ = "OA" GOTO 22000
20090 IF C$ = "OP" GOTO 23000
20100 IF C$ = "OL" GOTO 24000
20110 IF C$ = "OI" GOTO 25000
20120 IF C$ = "OU" GOTO 26000
20130 PRINT "COMANDO INVALIDO: ";C$: STOP
21000 REM EXAMINAR OBJETO
21010 TT = 0: GOSUB 200: PRINT : PRINT ">EXAMINAR ";O$(OX): PRINT
21020 IF OX = 3 THEN PRINT "AS BOLINHAS SE ESPALHAM PELO  CHAO.":O(3) = 0:O(6) = SX: GOTO 8
21030 PRINT "NADA A DECLARAR.": GOTO 8
21999 RETURN
22000 REM ACIONAR OBJETO
22010 TT = 0: GOSUB 200: PRINT : PRINT ">ACIONAR ";O$(OX): PRINT
22020 IF OX = 2 THEN IF O(6) = MU THEN PRINT "PARABENS!": PRINT "VOCE COMPLETOU A TAREFA!": PRINT : END
22030 IF OX = 2 THEN PRINT "TU CAIS DENTRO DO VASO E VAIS": PRINT "EMBORA COM A DESCARGA!": PRINT : END
22040 IF OX = 5 THEN PRINT "USA O REVOLVER EM ALGUEM.": GOTO 8
22050 IF OX = 7 THEN O(8) = O(7):O(7) = 0: PRINT "A LAMPADA ACENDE.": GOTO 8
22999 PRINT "NAO VEJO COMO.": GOTO 8
23000 REM PEGAR OBJETO
23010 TT = 0: GOSUB 200: PRINT : PRINT ">PEGAR ";O$(OX): PRINT
23020 IF OX = 3 OR OX = 4 OR OX = 5 OR OX = 6 OR OX = 7 OR OX = 8 THEN PRINT "OK.":O(OX) = MU: GOTO 8
23999 PRINT "ACHO QUE NAO.": GOTO 8
24000 REM LARGAR OBJETO
24010 TT = 0: GOSUB 200: PRINT : PRINT ">LARGAR ";O$(OX): PRINT
24020 IF OX = 3 OR OX = 4 OR OX = 5 OR OX = 6 OR OX = 7 OR OX = 8 THEN PRINT "OK.":O(OX) = SX: GOTO 8
24999 PRINT "ACHO QUE NAO.": GOTO 8
25000 REM IR PARA OBJETO
25010 TT = 0: GOSUB 200: PRINT : PRINT ">IR PARA ";O$(OX): PRINT
25020 IF OX = 1 THEN IF O(4) = MU THEN PRINT "QUE PENA, TU TE AFOGASTE!": PRINT : END
25030 IF OX = 1 THEN PRINT "ACHASTE UM REVOLVER!":O(5) = MU: GOTO 8
25999 PRINT "ACHO QUE NAO.": GOTO 8
26000 REM USAR OBJETO
26010 TT = 0: GOSUB 200: PRINT : PRINT ">USAR ";O$(OX): PRINT ">EM ";O$(OY): PRINT
26020 IF OX = 5 AND OY = 9 THEN PRINT "MATASTE O COLETOR DE IMPOSTOS.":O(9) = 0: GOTO 8
26999 PRINT "ACHO QUE NAO.": GOTO 8
30000 REM CHECAGENS POSTERIORES A EXECUCAO DO COMANDO
30010 IF O(9) = 0 THEN RETURN
30020 TT = 0: GOSUB 200: PRINT :O(9) = 0:IN = 0: FOR I = 1 TO OQ: IF O(I) = MU THEN IN = MU:I = OQ
30030 NEXT : IF IN THEN 30060
30040 PRINT "COMO NAO TINHAS NADA QUE PUDESSESER CONFISCADO, O COLETOR DE"
30050 PRINT "IMPOSTOS TE PRENDEU NUMA MASMOR-RA IMUNDA.": PRINT : END
30060 I = INT ( RND (1) * OQ) + 1: IF O(I) < > MU THEN 30060
30070 PRINT "O COLETOR DE IMPOSTOS TE TOMA": PRINT "O/A ";O$(I):O(I) = 0: GOSUB 8
39999 RETURN
50000 REM INICIALIZACAO
50010 READ SQ: DIM S$(SQ),SN(SQ),SS(SQ),SL(SQ),SO(SQ),SC(SQ),SB(SQ)
50020 FOR I = 1 TO SQ: READ S$(I),SN(I),SS(I),SL(I),SO(I),SC(I),SB(I): NEXT
50030 READ OQ: DIM O$(OQ),O(OQ)
50040 FOR I = 1 TO OQ: READ O$(I),O(I): NEXT
50050 SX = 1: REM ID DA SALA ATUAL
50060 CI = 0: REM COLETOR DE IMPOSTOS JA APARECEU?
50070 RETURN
60000 DATA 13
60001 DATA "ENTRADA DA CIDADE OCULTA",,,5,2,,
60002 DATA "ATALHO ENLAMEADO",3,,1,,,
60003 DATA "FLORESTA PETRIFICADA",,2,,4,,
60004 DATA "BEIRA DE UM GRANDE RIO",,,3,,,
60005 DATA "HALL DE ENTRADA",6,13,10,1,,
60006 DATA "SALA MUITO SUJA",7,5,8,,,
60007 DATA "DIANTE DE UMA GRANDE CONSTRUCAO",,6,,,,
60008 DATA "QUARTO ESCURO",,10,,6,,
60009 DATA "ESTA ESCURO DEMAIS PARA VER!",,,,,,
60010 DATA "PATIO",8,,11,5,,
60011 DATA "JARDIM",,12,,10,,
60012 DATA "SALA DO TRONO",11,,,,,
60013 DATA "GUARDA-LOUCAS",5,,,,,
61000 DATA 9
61001 DATA "RIO CAUDALOSO",4
61002 DATA "CORRENTE PEND. SOBRE O TRONO",12
61003 DATA "SACO DE BOLAS DE GUDE",7
61004 DATA "TIJOLO",2
61005 DATA "REVOLVER",
61006 DATA "OLHO CRAVEJADO DE BRILHANTES",
61007 DATA "LAMPADA APAGADA",13
61008 DATA "LAMPADA ACESA",
61009 DATA "COLETOR DE IMPOSTOS",
