10  HGR 
100 QZ = 1: FOR I = 0 TO 255
110 QX = (I AND 31) * 8:QY =  INT (I / 32) * 8
120 QC = I
130  GOSUB 64000
140  NEXT I
150  GOTO 150
63999  END 
64000  REM DESENHA CHR$(QC) EM QX,QY, APAGANDO ANTES ANTES SE QZ <> 0
64010  IF QZ THEN  FOR QY = QY TO QY + 7: SET QX,QY: UNDRAW 7,0: NEXT QY:QY = QY - 8
64020  IF QC < 33 THEN  RETURN 
64030  IF QC < 48 THEN  ON QC - 32 GOTO 65033,65034,65035,65036,65037,65038,65039,65040,65041,65042,65043,65044,65045,65046,65047
64040  IF QC < 64 THEN  ON QC - 47 GOTO 65048,65049,65050,65051,65052,65053,65054,65055,65056,65057,65058,65059,65060,65061,65062,65063
64050  IF QC < 80 THEN  ON QC - 63 GOTO 65064,65065,65066,65067,65068,65069,65070,65071,65072,65073,65074,65075,65076,65077,65078,65079
64060  IF QC < 96 THEN  ON QC - 79 GOTO 65080,65081,65082,65083,65084,65085,65086,65087,65088,65089,65090,65091,65092,65093,65094,65095
64070  RETURN 
64999  REM DESENHO DOS CARACTERES
65033  SET QX + 3,QY: DRAW 0,4: PLOT QX + 3,QY + 6: RETURN : REM !
65034  SET QX + 2,QY: DRAW 0,2: SET QX + 4,QY: DRAW 0,2: RETURN : REM "
65035  SET QX + 2,QY: DRAW 0,6: SET QX + 4,QY: DRAW 0,6: SET QX + 1,QY + 1: DRAW 4,0: SET QX + 1,QY + 5: DRAW 4,0: RETURN : REM #
65036  SET QX + 3,QY: DRAW 0,6: SET QX + 5,QY + 1: DRAW  - 4,1: DRAW 4,2: DRAW  - 4,1: RETURN : REM $
65037  SET QX + 1,QY + 1: DRAW 1,1: DRAW 1, - 1: DRAW  - 1, - 1: DRAW  - 1,1: SET QX + 1,QY + 4: DRAW 5, - 2: SET QX + 4,QY + 5: DRAW 1,1: DRAW 1, - 1: DRAW  - 1, - 1: DRAW  - 1,1: RETURN : REM %
65038  SET QX + 3,QY: DRAW 0,1: SET QX + 5,QY + 1: DRAW  - 4,1: DRAW 3,1: DRAW  - 3,1: DRAW 4,1: SET QX + 3,QY + 5: DRAW 0,1: RETURN : REM &
65039  SET QX + 3,QY: DRAW 0,2: RETURN : REM '
65040  SET QX + 5,QY: DRAW  - 2,2: DRAW 0,2: DRAW 2,2: RETURN : REM (
65041  SET QX + 1,QY: DRAW 2,2: DRAW 0,2: DRAW  - 2,2: RETURN : REM )
65042  SET QX + 1,QY + 1: DRAW 4,4: SET QX + 1,QY + 3: DRAW 4,0: SET QX + 1,QY + 5: DRAW 4, - 4: RETURN : REM *
65043  SET QX + 1,QY + 3: DRAW 4,0: SET QX + 3,QY + 1: DRAW 0,4: RETURN : REM +
65044  SET QX + 3,QY + 5: DRAW  - 1,2: RETURN : REM ,
65045  SET QX + 1,QY + 3: DRAW 4,0: RETURN : REM -
65046  PLOT QX + 3,QY + 6: RETURN : REM .
65047  SET QX + 1,QY + 6: DRAW 4, - 6: RETURN : REM /
65048  PLOT QX + 3,QY + 3: SET QX + 1,QY + 3: DRAW 2,2: DRAW 2, - 2: DRAW  - 2, - 2: DRAW  - 2,2: RETURN : REM 0
65049  SET QX + 1,QY + 2: DRAW 2, - 2: DRAW 0,6: RETURN : REM 1
65050  SET QX + 1,QY: DRAW 4,3: DRAW  - 4,3: DRAW 4,0: RETURN : REM 2
65051  SET QX + 1,QY: DRAW 4,1: DRAW  - 3,2: DRAW 3,2: DRAW  - 4,1: RETURN : REM 3
65052  SET QX + 4,QY + 6: DRAW 0, - 6: DRAW  - 3,3: DRAW 4,0: RETURN : REM 4
65053  SET QX + 4,QY: DRAW  - 4,0: DRAW 4,3: DRAW  - 4,3: RETURN : REM 5
65054  SET QX + 5,QY: DRAW  - 4,4: DRAW 2,2: DRAW 2, - 2: DRAW  - 2, - 2: RETURN : REM 6
65055  SET QX + 1,QY: DRAW 4,0: DRAW  - 4,6: RETURN : REM 7
65056  SET QX + 3,QY: DRAW 2,1: DRAW  - 4,4: DRAW 2,1: DRAW 2, - 1: DRAW  - 4, - 4: DRAW 2, - 1: RETURN : REM 8
65057  SET QX + 3,QY + 4: DRAW  - 2, - 2: DRAW 2, - 2: DRAW 2,2: DRAW  - 4,4: RETURN : REM 9
65058  PLOT QX + 3,QY + 2: PLOT QX + 3,QY + 4: RETURN : REM :
65059  PLOT QX + 3,QY + 2: SET QX + 3,QY + 5: DRAW  - 1,2: RETURN : REM ;
65060  SET QX + 4,QY: DRAW  - 4,3: DRAW 4,3: RETURN : REM <
65061  SET QX + 1,QY + 2: DRAW 4,0: SET QX + 1,QY + 4: DRAW 4,0: RETURN : REM =
65062  SET QX + 1,QY: DRAW 4,3: DRAW  - 4,3: RETURN : REM >
65063  SET QX + 1,QY: DRAW 4,2: DRAW  - 2,2: PLOT QX + 3,QY + 6: RETURN : REM ?
65064  SET QX + 5,QY + 3: DRAW  - 1,1: DRAW 1,1: DRAW 1,0: DRAW 0, - 5: DRAW  - 3,0: DRAW  - 3,3: DRAW 3,3: RETURN : REM @
65065  SET QX + 1,QY + 6: DRAW 2, - 6: DRAW 2,6: SET QX + 2,QY + 3: DRAW 2,0: RETURN : REM A
65066  SET QX + 1,QY: DRAW 4,1: DRAW  - 4,2: DRAW 4,2: DRAW  - 4,1: DRAW 0, - 6: RETURN : REM B
65067  SET QX + 5,QY: DRAW  - 4,3: DRAW 4,3: RETURN : REM C
65068  SET QX + 1,QY: DRAW 4,3: DRAW  - 4,3: DRAW 0, - 6: RETURN : REM D
65069  SET QX + 5,QY: DRAW  - 4,1: DRAW 3,2: DRAW  - 3,2: DRAW 4,1: RETURN : REM E
65070  SET QX + 1,QY + 6: DRAW 0, - 6: DRAW 4,0: SET QX + 1,QY + 3: DRAW 3,0: RETURN : REM F
65071  SET QX + 5,QY: DRAW  - 4,3: DRAW 4,3: DRAW 0, - 3: RETURN : REM G
65072  SET QX + 1,QY: DRAW 0,6: SET QX + 1,QY + 3: DRAW 4,0: SET QX + 5,QY: DRAW 0,6: RETURN : REM H
65073  SET QX + 3,QY: DRAW 0,6: RETURN : REM I
65074  SET QX + 5,QY: DRAW 0,6: DRAW  - 4, - 2: RETURN : REM J
65075  SET QX + 1,QY: DRAW 0,6: SET QX + 5,QY: DRAW  - 4,3: DRAW 4,3: RETURN : REM K
65076  SET QX + 1,QY: DRAW 0,6: DRAW 4,0: RETURN : REM L
65077  SET QX,QY + 6: DRAW 1, - 6: DRAW 2,3: DRAW 2, - 3: DRAW 1,6: RETURN : REM M
65078  SET QX + 1,QY + 6: DRAW 0, - 6: DRAW 4,6: DRAW 0, - 6: RETURN : REM N
65079  SET QX + 1,QY + 3: DRAW 2, - 2: DRAW 2,2: DRAW  - 2,2: DRAW  - 2, - 2: RETURN : REM O
65080  SET QX + 1,QY + 6: DRAW 0, - 6: DRAW 4,1: DRAW  - 4,2: RETURN : REM P
65081  SET QX + 2,QY + 6: DRAW 3, - 3: DRAW  - 2, - 2: DRAW  - 2,2: DRAW 3,3: DRAW 1,0: RETURN : REM Q
65082  SET QX + 1,QY + 6: DRAW 0, - 6: DRAW 4,1: DRAW  - 4,2: DRAW 4,3: RETURN : REM R
65083  SET QX + 5,QY: DRAW  - 4,1: DRAW 4,4: DRAW  - 4,1: RETURN : REM S
65084  SET QX + 1,QY: DRAW 4,0: SET QX + 3,QY: DRAW 0,6: RETURN : REM T
65085  SET QX + 1,QY: DRAW 0,6: DRAW 4,0: DRAW 0, - 6: RETURN : REM U
65086  SET QX + 1,QY: DRAW 2,6: DRAW 2, - 6: RETURN : REM V
65087  SET QX,QY: DRAW 1,6: DRAW 2, - 3: DRAW 2,3: DRAW 1, - 6: RETURN : REM W
65088  SET QX + 1,QY: DRAW 4,6: SET QX + 1,QY + 6: DRAW 4, - 6: RETURN : REM X
65089  SET QX + 1,QY: DRAW 2,3: SET QX + 1,QY + 6: DRAW 4, - 6: RETURN : REM Y
65090  SET QX + 1,QY: DRAW 4,0: DRAW  - 4,6: DRAW 4,0: RETURN : REM Z
65091  SET QX + 5,QY: DRAW  - 3,0: DRAW 0,6: DRAW 3,0: RETURN : REM [
65092  SET QX + 1,QY: DRAW 4,6: RETURN : REM \
65093  SET QX + 1,QY: DRAW 2,0: DRAW 0,6: DRAW  - 2,0: RETURN : REM ]
65094  SET QX + 1,QY + 2: DRAW 2, - 2: DRAW 2,2: RETURN : REM ^
65095  SET QX,QY + 7: DRAW 7,0: RETURN : REM _
