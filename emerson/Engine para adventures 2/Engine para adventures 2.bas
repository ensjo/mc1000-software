0  REM ENGINE PARA ADVENTURES POR MENUS - EMERSON JOSE SILVEIRA DA COSTA (ENSJO) - 2013-03-28.
1  TEXT : POKE 856,2: DIM AA(23),AA$(8):AA$(1) =  CHR$ (27) + "=": GOTO 100
2  ON AA(0) GOTO 10,24,9: STOP
3  PRINT AA$(1); CHR$ ( PEEK (857)); CHR$ (32 +  PEEK (858));: POKE 910,0: RETURN
4  CALL 49158: IF  PEEK (284) = 3 THEN  STOP
5 AA$(4) =  CHR$ ( PEEK (284)):AA(10) =  VAL (AA$(4)): RETURN
6  GOSUB 4: FOR AA = 1 TO  LEN (AA$(3)): IF AA$(4) =  MID$ (AA$(3),AA,1) THEN  RETURN
7  NEXT : GOTO 6
8  GOSUB 3: PRINT  MID$ (AA$(2),2);:AA$(3) = AA$(3) +  LEFT$ (AA$(2),1): RETURN
9  SET 28,15: GOSUB 3: PRINT "OK:";: CALL 49158: RETURN
10 AA(0) = 4: GOSUB 51:AA(4) = AA(2):AA(5) = AA(3):AA$(6) = AA$(0):AA(6) = 1
11 AA(7) = 0
12 AA(0) = 1: GOSUB 51:AA(20) = AA(1):AA(21) = AA(2):AA$(7) = AA$(0)
13  GOSUB 25: IF AA$(4) = "." GOTO 13
14  IF AA$(4) = "@" THEN AA(6) = AA(6) - 1: GOTO 11
15  IF AA(6) = 1 OR AA(6) = 3 THEN  IF AA$(4) = "I" OR AA$(4) = "V" THEN AA(7) =  NOT AA(7): GOTO 12
16  ON AA(6) GOTO 17,19,21: STOP
17  IF AA(10) THEN AA(0) = 3:AA(1) = AA(10 + AA(10)): GOSUB 51:AA(22) = AA(1):AA(23) = AA(2):AA$(8) = AA$(0):AA(6) = 2: GOTO 11
18 AA$(5) = AA$(4): GOTO 23
19 AA$(5) = "O" + AA$(4): IF AA$(4) = "U" THEN AA(6) = 3: GOTO 11
20  GOTO 22
21 AA(2) = AA(10 + AA(10))
22 AA(1) = AA(22)
23 AA$(0) = AA$(5): RETURN
24 AA(6) = 0
25  HOME : POKE 910,0: INVERSE : PRINT AA$(6); TAB( 32);: IF AA(6) = 2 OR AA(6) = 3 THEN  PRINT AA$(8); TAB( 64);: IF AA(6) = 3 THEN  PRINT "USAR EM"; TAB( 96);
26  NORMAL : POKE 910,0: IF AA(6) = 0 THEN  RETURN
27 AA$(3) = "": ON AA(6) GOSUB 30,38,36: IF AA(6) = 2 OR AA(6) = 3 THEN  SET 12,15:AA$(2) = "@@=CANCELAR": GOSUB 8
28  IF AA(6) = 1 OR AA(6) = 3 THEN  GOSUB 42
29  SET 25,15: GOSUB 3: PRINT "OPCAO:";: GOTO 6
30  IF AA(5) AND 1 THEN  SET 0,1:AA$(2) = "NN=NORTE": GOSUB 8
31  IF AA(5) AND 2 THEN  SET 8,1:AA$(2) = "SS=SUL": GOSUB 8
32  IF AA(5) AND 4 THEN  SET 16,1:AA$(2) = "LL=LESTE": GOSUB 8
33  IF AA(5) AND 8 THEN  SET 24,1:AA$(2) = "OO=OESTE": GOSUB 8
34  IF AA(5) AND 16 THEN  SET 0,2:AA$(2) = "CC=CIMA": GOSUB 8
35  IF AA(5) AND 32 THEN  SET 8,2:AA$(2) = "BB=BAIXO": GOSUB 8
36  SET 0,3: IF AA(7) THEN AA$(2) = "VV=COISAS A VISTA": GOSUB 8: SET 0,5: GOSUB 3: PRINT "INVENTARIO:";: RETURN
37 AA$(2) = "II=INVENTARIO": GOSUB 8: SET 0,5: GOSUB 3: PRINT "COISAS A VISTA:";: RETURN
38 AA(7) = AA(23) =  - 1: SET 0,2:AA$(2) = "EE=EXAMINAR": GOSUB 8: SET 12,2:AA$(2) = "AA=ACIONAR": GOSUB 8: SET 24,2: GOSUB 3: IF AA(7) THEN AA$(2) = "LL=LARGAR": GOSUB 8:
39  IF  NOT AA(7) THEN AA$(2) = "PP=PEGAR": GOSUB 8
40  SET 0,3:AA$(2) = "UU=USAR EM": GOSUB 8: IF  NOT AA(7) THEN  SET 12,3:AA$(2) = "II=IR PARA": GOSUB 8
41  RETURN
42 AA(9) = 0:AA(0) = 1: GOSUB 51: IF AA(20) =  - 1 THEN AA(20) = AA(1):AA(21) = AA(2):AA$(7) = AA$(0)
43 AA(8) = AA(20) <  > AA(1)
44  IF AA(20) =  - 1 GOTO 48
45  IF  NOT (( NOT AA(7) AND AA(21) = AA(4)) OR (AA(7) AND AA(21) =  - 1)) THEN AA(0) = 2:AA(1) = AA(20): GOSUB 51:AA(20) = AA(1):AA(21) = AA(2):AA$(7) = AA$(0): GOTO 44
46 AA(9) = AA(9) + 1: IF AA(9) = 10 THEN AA(8) =  - 1: GOTO 48
47 AA(10 + AA(9)) = AA(20): SET 0,5 + AA(9):AA$(2) =  CHR$ (48 + AA(9)) +  CHR$ (48 + AA(9)) + "=" + AA$(7): GOSUB 8:AA(0) = 2:AA(1) = AA(20): GOSUB 51:AA(20) = AA(1):AA(21) = AA(2):AA$(7) = AA$(0): GOTO 44
48  IF AA(8) THEN  SET 0,15:AA$(2) = "..=+ITENS": GOSUB 8
49  RETURN
50  REM ROTINAS DE DELEGACAO (SEGUNDO A AVENTURA)
51  ON AA(0) GOTO 52,53,54,55: STOP
52 AA(1) = 1: GOTO 54
53 AA(1) = AA(1) + 1: IF AA(1) > OQ THEN AA(1) =  - 1:AA(2) =  - 1:AA$(0) = "": RETURN
54 AA(2) = O(AA(1)):AA$(0) = O$(AA(1)): RETURN
55 AA(2) = SX:AA(3) = 0: IF SN(SX) THEN AA(3) = AA(3) OR 1
56  IF SS(SX) THEN AA(3) = AA(3) OR 2
57  IF SL(SX) THEN AA(3) = AA(3) OR 4
58  IF SO(SX) THEN AA(3) = AA(3) OR 8
59  REM IF SC(SX) THEN AA(3) = AA(3) OR 16
60  REM IF SB(SX) THEN AA(3) = AA(3) OR 32
61 AA$(0) = S$(SX): RETURN
100  REM AVENTURA
1000  REM AVENTURA INPUT ADAPTADA
1010  GOSUB 50000: REM INICIALIZACAO
1020  GOSUB 10000: REM CHECAGENS ANTERIORES A EXIBICAO DA SALA
1030 AA(0) = 1: GOSUB 2: REM (ENGINE DE INTERFACE) EXIBE SALA E AGUARDA COMANDO
1035 C$ = AA$(0):OX = AA(1):OY = AA(2)
1040  GOSUB 20000: REM INTERPRETA COMANDO
1050  GOSUB 30000 REM CHECAGENS POSTERIORES A EXECUCAO DO COMANDO
1060  GOTO 1020
10000  REM CHECAGENS ANTERIORES A EXIBICAO DA SALA
10010  IF SX = 8 THEN  IF  NOT (O(8) =  - 1 OR O(8) = SX) THEN SX = 9
10020  IF SX = 9 THEN  IF (O(8) =  - 1 OR O(8) = SX) THEN SX = 8
10030  IF SX = 9 THEN  FOR I = 1 TO OQ: IF O(I) = 9 THEN O(I) = 8
10040  IF SX = 9 THEN  NEXT I
10050  IF SX <  > 9 THEN  IF  NOT CI THEN  IF  RND (1) < .07 THEN O(9) = SX:CI =  - 1
10060  RETURN
20000  REM INTERPRETA COMANDO
20010  IF C$ = "N" THEN SX = SN(SX): RETURN
20020  IF C$ = "S" THEN SX = SS(SX): RETURN
20030  IF C$ = "L" THEN SX = SL(SX): RETURN
20040  IF C$ = "O" THEN SX = SO(SX): RETURN
20070  IF C$ = "OE" GOTO 21000
20080  IF C$ = "OA" GOTO 22000
20090  IF C$ = "OP" GOTO 23000
20100  IF C$ = "OL" GOTO 24000
20110  IF C$ = "OI" GOTO 25000
20120  IF C$ = "OU" GOTO 26000
20130  PRINT "COMANDO INVALIDO: ";C$: STOP
21000  REM EXAMINAR OBJETO
21010 AA(0) = 2: GOSUB 2: PRINT : PRINT ">EXAMINAR ";O$(OX): PRINT
21020  IF OX = 3 THEN  PRINT "AS BOLINHAS SE ESPALHAM PELO  CHAO.":O(3) = 0:O(6) = SX:AA(0) = 3: GOTO 2
21030  PRINT "NADA A DECLARAR.":AA(0) = 3: GOTO 2
21999  RETURN
22000  REM ACIONAR OBJETO
22010 AA(0) = 2: GOSUB 2: PRINT : PRINT ">ACIONAR ";O$(OX): PRINT
22020  IF OX = 2 THEN  IF O(6) =  - 1 THEN  PRINT "PARABENS!": PRINT "VOCE COMPLETOU A TAREFA!": PRINT : END
22030  IF OX = 2 THEN  PRINT "TU CAIS DENTRO DO VASO E VAIS": PRINT "EMBORA COM A DESCARGA!": PRINT : END
22040  IF OX = 5 THEN  PRINT "USA O REVOLVER EM ALGUEM.":AA(0) = 3: GOTO 2
22050  IF OX = 7 THEN O(8) = O(7):O(7) = 0: PRINT "A LAMPADA ACENDE.":AA(0) = 3: GOTO 2
22999  PRINT "NAO VEJO COMO.":AA(0) = 3: GOTO 2
23000  REM PEGAR OBJETO
23010 AA(0) = 2: GOSUB 2: PRINT : PRINT ">PEGAR ";O$(OX): PRINT
23020  IF OX = 3 OR OX = 4 OR OX = 5 OR OX = 6 OR OX = 7 OR OX = 8 THEN O(OX) =  - 1:AA(0) = 3: GOTO 2
23999  PRINT "ACHO QUE NAO.":AA(0) = 3: GOTO 2
24000  REM LARGAR OBJETO
24010 AA(0) = 2: GOSUB 2: PRINT : PRINT ">LARGAR ";O$(OX): PRINT
24020 O(OX) = SX:AA(0) = 3: GOTO 2
24999  PRINT "ACHO QUE NAO.":AA(0) = 3: GOTO 2
25000  REM IR PARA OBJETO
25010 AA(0) = 2: GOSUB 2: PRINT : PRINT ">IR PARA ";O$(OX): PRINT
25020  IF OX = 1 THEN  IF O(4) =  - 1 THEN  PRINT "QUE PENA, TU TE AFOGASTE!": PRINT : END
25030  IF OX = 1 THEN  PRINT "ACHASTE UM REVOLVER!":O(5) =  - 1:AA(0) = 3: GOTO 2
25999  PRINT "ACHO QUE NAO.":AA(0) = 3: GOTO 2
26000  REM USAR OBJETO
26010 AA(0) = 2: GOSUB 2: PRINT : PRINT ">USAR ";O$(OX): PRINT ">EM ";O$(OY): PRINT
26020  IF OX = 5 AND OY = 9 THEN  PRINT "MATASTE O COLETOR DE IMPOSTOS.":O(9) = 0:AA(0) = 3: GOTO 2
26999  PRINT "ACHO QUE NAO.":AA(0) = 3: GOTO 2
30000  REM CHECAGENS POSTERIORES A EXECUCAO DO COMANDO
30010  IF O(9) = 0 THEN  RETURN
30020 AA(0) = 2: GOSUB 2: PRINT :O(9) = 0:IN = 0: FOR I = 1 TO OQ: IF O(I) =  - 1 THEN IN =  - 1:I = OQ
30030  NEXT : IF IN THEN 30060
30040  PRINT "COMO NAO TINHAS NADA QUE PUDESSESER CONFISCADO, O COLETOR DE"
30050  PRINT "IMPOSTOS TE PRENDEU NUMA MASMOR-RA IMUNDA.": PRINT : END
30060 I =  INT ( RND (1) * OQ) + 1: IF O(I) <  >  - 1 THEN 30060
30070  PRINT "O COLETOR DE IMPOSTOS TE TOMA": PRINT "O/A ";O$(I):O(I) = 0:AA(0) = 3: GOTO 2
39999  RETURN
50000  REM INICIALIZACAO
50010  READ SQ: DIM S$(SQ),SN(SQ),SS(SQ),SL(SQ),SO(SQ)
50020  FOR I = 1 TO SQ: READ S$(I),SN(I),SS(I),SL(I),SO(I): NEXT
50030  READ OQ: DIM O$(OQ),O(OQ)
50040  FOR I = 1 TO OQ: READ O$(I),O(I): NEXT
50050 SX = 1: REM ID DA SALA ATUAL
50060 CI = 0: REM COLETOR DE IMPOSTOS JA APARECEU?
50070  RETURN
60000  DATA 13
60001  DATA "ENTRADA DA CIDADE OCULTA",,,5,2
60002  DATA "ATALHO ENLAMEADO",3,,1,
60003  DATA "FLORESTA PETRIFICADA",,2,,4
60004  DATA "BEIRA DE UM GRANDE RIO",,,3,
60005  DATA "HALL DE ENTRADA",6,13,10,1
60006  DATA "SALA MUITO SUJA",7,5,8,
60007  DATA "DIANTE DE UMA GRANDE CONSTRUCAO",,6,,
60008  DATA "QUARTO ESCURO",,10,,6
60009  DATA "ESTA ESCURO DEMAIS PARA VER!",,,,
60010  DATA "PATIO",8,,11,5
60011  DATA "JARDIM",,12,,10
60012  DATA "SALA DO TRONO",11,,,
60013  DATA "GUARDA-LOUCAS",5,,,
61000  DATA 9
61001  DATA "RIO CAUDALOSO",4
61002  DATA "CORRENTE PEND. SOBRE O TRONO",12
61003  DATA "SACO DE BOLAS DE GUDE",7
61004  DATA "TIJOLO",2
61005  DATA "REVOLVER",
61006  DATA "OLHO CRAVEJADO DE BRILHANTES",
61007  DATA "LAMPADA APAGADA",13
61008  DATA "LAMPADA ACESA",
61009  DATA "COLETOR DE IMPOSTOS",
