10  REM TRAVESSIA
20  TEXT : HOME : NORMAL
30  GOSUB 10000: REM INICIALIZACOES
40  GOSUB 20000: REM RESOLVE O PROBLEMA
50  GOSUB 30000: REM EXIBE SOLUCAO
60  END
10000  REM INICIALIZACOES
10010 ME = 1:MD = 2: REM MARGEM ESQUERDA E DIREITA
10020  DEF  FN MO(M) = ME + MD - M: REM CALCULA MARGEM OPOSTA
10030 IM = 10: REM INDICE MAXIMO
10040  DIM M$(IM): REM DESCRICAO DOS MOVIMENTOS
10050  DIM H(IM),L(IM),C(IM),A(IM): REM POSICOES DO HOMEM, DO LOBO, DO CARNEIRO E DA ALFACE
10060 M$(0) = "SITUACAO INICIAL"
10070 H(0) = ME:L(0) = ME:C(0) = ME:A(0) = ME: REM SITUACAO INICIAL: TODOS NA MARGEM ESQUERDA
10080 I = 0: REM INDICE
10090 IO = 0: REM INDICE QUANDO O OBJETIVO FOR ATINGIDO
10100  RETURN
20000  REM BUSCA SOLUCAO A PARTIR DA SITUACAO ATUAL
20010 I = I + 1: REM NOVO MOVIMENTO
20020  GOSUB 21000: REM BUSCA SOLUCAO ATRAVESSANDO SOZINHO
20030  IF IO = 0 THEN  GOSUB 22000: REM BUSCA SOLUCAO ATRAVESSANDO COM LOBO
20040  IF IO = 0 THEN  GOSUB 23000: REM BUSCA SOLUCAO ATRAVESSANDO COM CARNEIRO
20050  IF IO = 0 THEN  GOSUB 24000: REM BUSCA SOLUCAO ATRAVESSANDO COM ALFACE
20060 I = I - 1
20070  RETURN
21000  REM BUSCA SOLUCAO ATRAVESSANDO SOZINHO
21010 M$(I) = "ATRAVESSA SOZINHO": REM DESCREVE MOVIMENTO
21020  GOSUB 25000: REM CALCULA NOVA SITUACAO
21030  GOSUB 26000: REM CHECA VIABILIDADE E SEGUIMENTO
21040  RETURN
22000  REM BUSCA SOLUCAO ATRAVESSANDO COM LOBO
22010  IF H(I - 1) <  > L(I - 1) THEN  RETURN : REM DEVEM ESTAR NA MESMA MARGEM
22020 M$(I) = "LEVA LOBO": REM DESCREVE MOVIMENTO
22030  GOSUB 25000:L(I) =  FN MO(L(I - 1)): REM CALCULA NOVA SITUACAO
22040  GOSUB 26000: REM CHECA VIABILIDADE E SEGUIMENTO
22050  RETURN
23000  REM BUSCA SOLUCAO ATRAVESSANDO COM CARNEIRO
23010  IF H(I - 1) <  > C(I - 1) THEN  RETURN : REM DEVEM ESTAR NA MESMA MARGEM
23020 M$(I) = "LEVA CARNEIRO": REM DESCREVE MOVIMENTO
23030  GOSUB 25000:C(I) =  FN MO(C(I - 1)): REM CALCULA NOVA SITUACAO
23040  GOSUB 26000: REM CHECA VIABILIDADE E SEGUIMENTO
23050  RETURN
24000  REM BUSCA SOLUCAO ATRAVESSANDO COM ALFACE
24010  IF H(I - 1) <  > A(I - 1) THEN  RETURN : REM DEVEM ESTAR NA MESMA MARGEM
24020 M$(I) = "LEVA ALFACE": REM DESCREVE MOVIMENTO
24030  GOSUB 25000:A(I) =  FN MO(A(I - 1)): REM CALCULA NOVA SITUACAO
24040  GOSUB 26000: REM CHECA VIABILIDADE E SEGUIMENTO
24050  RETURN
25000  REM CALCULA TRAVESSIA DO HOMEM SOZINHO (BASE PARA TODOS OS MOVIMENTOS)
25010 H(I) =  FN MO(H(I - 1)):L(I) = L(I - 1):C(I) = C(I - 1):A(I) = A(I - 1)
25020  RETURN
26000  REM CHECA VIABILIDADE E SEGUIMENTO DA SITUACAO ATUAL
26010  PRINT  TAB( I);M$(I);"?"
26020  GOSUB 27000: IF IO THEN  RETURN : REM CHECA ATINGIMENTO DO OBJETIVO
26030  GOSUB 28000: IF F THEN  RETURN : REM CHECA SITUACOES FATAIS
26040  GOSUB 29000: IF F THEN  RETURN : REM CHECA SITUACAO JA OCORRIDA
26050  PRINT  TAB( I);"VEJAMOS..."
26060  GOSUB 20000: REM SEM PROBLEMAS COM ESTE MOVIMENTO, BUSCAR SOLUCAO A PARTIR DAQUI
26070  RETURN
27000  REM CHECA ATINGIMENTO DO OBJETIVO
27010  IF H(I) = MD AND L(I) = MD AND C(I) = MD AND A(I) = MD THEN  PRINT  TAB( I);"OBJETIVO ATINGIDO!":IO = I: RETURN
27020  RETURN
28000  REM CHECA SITUACOES FATAIS
28010 F = 0
28020  IF H(I) <  > C(I) AND L(I) = C(I) THEN  PRINT  TAB( I);"NAO: LOBO COME CARNEIRO!":F = 1: RETURN
28030  IF H(I) <  > C(I) AND C(I) = A(I) THEN  PRINT  TAB( I);"NAO: CARNEIRO COME ALFACE!":F = 1: RETURN
28040  RETURN
29000  REM CHECA SE A SITUACAO ATUAL JA OCORREU (PARA EVITAR LACO INFINITO)
29010 F = 0
29020  FOR J = 0 TO I - 1
29030  IF H(I) = H(J) AND L(I) = L(J) AND C(I) = C(J) AND A(I) = A(J) THEN  PRINT  TAB( I);"NAO: SITUACAO JA OCORREU.":F = 1: RETURN
29040  NEXT J
29050  RETURN
30000  REM EXIBE SOLUCAO
30010  PRINT : PRINT "*** SOLUCAO ***": PRINT
30020  FOR I = 0 TO IO
30030  PRINT  MID$ (" H",1 +  ABS (H(I) = ME),1);
30040  PRINT  MID$ (" L",1 +  ABS (L(I) = ME),1);
30050  PRINT  MID$ (" C",1 +  ABS (C(I) = ME),1);
30060  PRINT  MID$ (" A",1 +  ABS (A(I) = ME),1);
30070  PRINT " ";: INVERSE : PRINT "RIO";: NORMAL : PRINT " ";
30080  PRINT  MID$ (" H",1 +  ABS (H(I) = MD),1);
30090  PRINT  MID$ (" L",1 +  ABS (L(I) = MD),1);
30100  PRINT  MID$ (" C",1 +  ABS (C(I) = MD),1);
30110  PRINT  MID$ (" A",1 +  ABS (A(I) = MD),1);
30120  PRINT " ";M$(I)
30130  NEXT I
30140  RETURN
