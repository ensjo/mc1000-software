1  REM AMAZING - GERADOR DE LABIRINTOS POR BARRYM 2010-04-02.
2  REM CAVA CAMINHOS ALEATORIOS ATRAVES DE UM BLOCO DE PIXELS PARA FORMAR
3  REM UM LABIRINTO. USA A MEMORIA DE VIDEO PARA ARMAZENAR O LABIRINTO!
100  TEXT : HOME : INPUT "FORNECA AS DIMENSOES DESEJADAS  DO LABIRINTO (MAX 42,31)";H,V
110  IF (H < 1) OR (V < 1) OR (H > 42) OR (V > 31) THEN 100
190  GR : COLOR  = 1
199  REM INICIALIZA A FUNDACAO.
200  FOR Y = 1 TO 1 + V * 2: PLOT 1,Y TO 1 + H * 3,Y: NEXT Y
209  REM MOSTRA A ENTRADA
210 X =  INT ( RND (1) * H + 1):Y = 1: UNPLOT X * 3,1: UNPLOT X * 3 - 1,1
219  REM PREENCHE O LABIRINTO COM CAMINHOS ATE QUE TODAS AS CELULAS TENHAM SIDO LIMPAS.
220 C = H * V
229  REM LIMPA A CELULA ATUAL.
230  UNPLOT X * 3,Y * 2: UNPLOT X * 3 - 1,Y * 2:C = C - 1: IF C < 1 GOTO 310
234  REM APENAS VISITA A CELULA ATUAL.
235 M = X * 3:N = Y * 2
239  REM EXAMINA A AREA CIRCUNDANTE.
240 PX = M - 3:PY = N: GOSUB 1000:L = (X > 1) AND P:R = 0: IF (X < H) THEN PX = M + 2:PY = N: GOSUB 1000:R = P
245 PX = M:PY = N - 2: GOSUB 1000:U = (Y > 1) AND P:PX = M:PY = N + 2: GOSUB 1000:D = (Y < V) AND P
250  IF L OR R OR U OR D THEN 290
259  REM SE NAO TIVER SAIDA, PULA PARA UMA OUTRA CELULA VALIDA.
260 Y = Y + 1: IF Y > V THEN Y = 1:X = X + 1: IF X > H THEN X = 1
265 PX = X * 3:PY = Y * 2: GOSUB 1000: IF P THEN 260
270  GOTO 235
289  REM DERRUBA UMA PAREDE EM UMA DIRECAO ALEATORIA MAS VALIDA.
290  IF L AND ( INT ( RND (1) * 4) = 0) THEN  UNPLOT M - 2,N:X = X - 1: GOTO 230
292  IF R AND ( INT ( RND (1) * 4) = 0) THEN  UNPLOT M + 1,N:X = X + 1: GOTO 230
294  IF U AND ( INT ( RND (1) * 4) = 0) THEN  UNPLOT M,N - 1: UNPLOT M - 1,N - 1:Y = Y - 1: GOTO 230
296  IF D AND ( INT ( RND (1) * 4) = 0) THEN  UNPLOT M,N + 1: UNPLOT M - 1,N + 1:Y = Y + 1: GOTO 230
298  GOTO 290
309  REM MOSTRA A SAIDA.
310 X =  INT ( RND (1) * H + 1): UNPLOT X * 3 - 1,1 + V * 2: UNPLOT X * 3,1 + V * 2
970  POKE 284,0
980  IF  PEEK (284) = 0 THEN 980
990  RUN 
1000  REM IMPLEMENTACAO DA FUNCAO POINT(PX,PY) DO TRS-80 NO MC-1000.
1010  REM RETORNA VERDADEIRO (-1) SE A COORDENADA ESTIVER PLOTADA.
1020  OUT 128, PEEK (245) AND 254: REM HABILITA VRAM.
1030 P =  PEEK (32768 + 32 * PY +  INT (PX / 4))
1040  OUT 128, PEEK (245) OR 1: REM DESABILITA VRAM.
1050  IF P THEN P = P AND 3 * 4 ^ (3 - (PX AND 3)): IF P THEN P =  - 1
1060  RETURN 
