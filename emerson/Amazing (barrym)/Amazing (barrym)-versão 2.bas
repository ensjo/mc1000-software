0  GOTO 10
1  REM RETORNA EM P A COR DO PONTO NAS COORDENADAS (PX,PY).
2  IF VR THEN  OUT 128,136:P = ( PEEK (VR + 32 * PY +  INT (PX / 4)) / DV(PX AND 3)) AND 3: OUT 128,137: RETURN
3 VR = 32768: REM ENDERECO DA VRAM
4  DIM DV(3):DV(0) = 64:DV(1) = 16:DV(2) = 4:DV(3) = 1: REM DIVISORES
5  GOTO 2
10  HOME
11  INVERSE : PRINT  TAB( 12);"AMAZING"; TAB( 32);: NORMAL
12  PRINT "     GERADOR DE LABIRINTOS"
13  PRINT "     POR BARRYM 2010-04-02
14  PRINT " ADAPTADO AO MC-1000 POR ENSJO"
15  PRINT
16  PRINT "  USA A MEMORIA DE VIDEO PARA"
17  PRINT "     ARMAZENAR O LABIRINTO!"
18  PRINT
100  INPUT "FORNECA AS DIMENSOES DESEJADAS  DO LABIRINTO (MAX 42,31)";H,V
110 H =  INT (H):V =  INT (V): IF (H < 1) OR (V < 1) OR (H > 42) OR (V > 31) THEN  PRINT  CHR$ (7);"VALORES FORA DOS LIMITES!": GOTO 100
200 CP = 1:CC = 2: REM COR DA PAREDE E COR DO CAMINHO.
210  GR : COLOR  = CP: FOR Y = 1 TO 1 + V * 2: PLOT 1,Y TO 1 + H * 3,Y: NEXT Y: REM PREENCHE TODO O ESPACO COM A COR DA PAREDE.
220 X =  INT ( RND (1) * H + 1):Y = 1: COLOR  = CC: PLOT X * 3,1: DRAW  - 1,0: REM MOSTRA A ENTRADA.
230 C = H * V: REM QUANTIDADE DE CELULAS A LIMPAR.
300  REM PREENCHE O LABIRINTO COM CAMINHOS ATE QUE TODAS AS CELULAS TENHAM SIDO LIMPAS.
310 M = X * 3:N = Y * 2: REM COODENADAS DA CELULA NA TELA.
320  PLOT M,N: DRAW  - 1,0: REM LIMPA A CELULA ATUAL.
330 C = C - 1: IF C = 0 THEN 710: REM TERMINA SE FOI A ULTIMA CELULA.
340 DT = 4: REM QUANTIDADE DE DIRECOES A TENTAR.
350 DI =  INT ( RND (1) * 4): REM ESCOLHE ALEATORIAMENTE UMA DIRECAO A SEGUIR.
400  ON DI GOTO 440,470,500: REM 0=CIMA, 1=DIREITA, 2=BAIXO, 3=ESQUERDA.
410  IF Y = 1 THEN 600: REM BORDA ACIMA.
420 PX = M:PY = N - 2: GOSUB 2: IF P = CC THEN 600: REM CAMINHO ACIMA.
430  PLOT M,N - 1: DRAW  - 1,0:Y = Y - 1: GOTO 310: REM CIMA LIVRE: QUEBRA PAREDE E AVANCA.
440  IF X = H THEN 600: REM BORDA A DIREITA.
450 PX = M + 3:PY = N: GOSUB 2: IF P = CC THEN 600: REM CAMINHO A DIREITA.
460  PLOT M + 1,N:X = X + 1: GOTO 310: REM DIREITA LIVRE: QUEBRA PAREDE E AVANCA.
470  IF Y = V THEN 600: REM BORDA ABAIXO.
480 PX = M:PY = N + 2: GOSUB 2: IF P = CC THEN 600: REM CAMINHO ABAIXO.
490  PLOT M,N + 1: DRAW  - 1,0:Y = Y + 1: GOTO 310: REM BAIXO LIVRE: QUEBRA PAREDE E AVANCA.
500  IF X = 1 THEN 600: REM BORDA A ESQUERDA.
510 PX = M - 3:PY = N: GOSUB 2: IF P = CC THEN 600: REM CAMINHO A ESQUERDA.
520  PLOT M - 2,N:X = X - 1: GOTO 310: REM ESQUERDA LIVRE: QUEBRA PAREDE AVANCA.
600 DT = DT - 1: IF DT THEN DI = (DI + 1) AND 3: GOTO 400: REM TENTA OUTRA DIRECAO.
610  REM TODAS AS DIRECOES OBSTRUIDAS. PROCURA OUTRA CELULA LIMPA.
620 Y = Y + 1: IF Y > V THEN Y = 1:X = X + 1: IF X > H THEN X = 1
630 PX = X * 3:PY = Y * 2: GOSUB 2: IF P = CP THEN 620
640 M = PX:N = PY: GOTO 340
700  REM LABIRINTO PRONTO: MOSTRA A SAIDA.
710 X =  INT ( RND (1) * H + 1): PLOT X * 3 - 1,1 + V * 2: DRAW  - 1,0
800  REM AGUARDA TECLA E REINICIA.
810  POKE 283,0
820  IF  PEEK (283) = 0 THEN 820
830  RUN
